#INCAPACIDADES
#VERSION 2.600
#USUARIO =*
#NO EDITAR
#GRAFICA
#DESCRIPCION
#Este reporte muestra las incapacidades de los trabajadores a un rango de fechas.
#Este reporte imprime de los trabajadores la clave y nombre, de las incapacidades imprime, la fecha inicio, termino, duración y tipo.
#Este reporte pide la fecha inicial, final, tipo de falta y si se desea gráfica.
#Este reporte tiene los filtros estandares los cuales pueden filtrar cualquiera de los indices de Registro patronal, Sucursal
#Centro de Costo, Departamento, Puesto, Tipo Empleado y Tipo de Nómina.
#Este reporte permite generar grafica estadistica.
#FIN_DESCRIPCION

NUEVO_PARAMETRO_SAL ( 'SALIDA', 'PANTALLA/IMPRESORA/DISCO/EXCEL', $salida )
NUEVO_PARAMETRO_FEC ( 'FECHA INICIAL' , '', _fi )
NUEVO_PARAMETRO_FEC ( 'FECHA FINAL  ' , '', _ff )
LEE_PARAMETROS
BORRA_PARAMETROS

NUEVO_PARAMETRO_CHEQ( 'INCLUIR' , 'ENFERMEDAD GENERAL', _eg )
NUEVO_PARAMETRO_CHEQ( ''        , 'RIESGO DE TRABAJO' , _rt  )
NUEVO_PARAMETRO_CHEQ( ''        , 'ACCIDENTE EN TRAYECTO', _at )
NUEVO_PARAMETRO_CHEQ( ''        , 'PRE-MATERNIDAD', _pm )
NUEVO_PARAMETRO_CHEQ( ''        , 'MATERNIDAD DE ENLACE', _me )
NUEVO_PARAMETRO_CHEQ( ''        , 'POST-MATERNIDAD', _om )
NUEVO_PARAMETRO_CHEQ( 'GRAFICA' , '', _graficar )
LEE_PARAMETROS
BORRA_PARAMETROS

SI ( $salida = 'EXCEL' )
  SALIDA := 'PANTALLA'
SI_NO
  SALIDA := $salida
FIN_SI

INCLUYE ENCPIE.DAT

ENCABEZADO
   $titulo_reporte := 'Incapacidades del '+ FECHA (_fi)+ ' al ' + FECHA (_ff)
   encabezado_ini
   IMP ( COL(1), 'CLAVE', COL(10), 'NOMBRE', COL ( 47 ), 'DURACION' ) ;; IMPRIME
   IMP ( COL(10), 'INICIO', COL(23), 'TERMINO', COL(36), 'NUMERO', COL ( 46 ), 'TOT EN PER', COL(57), 'TIPO', COL ( 65 ), 'CASO' ) ;; IMPRIME
   encabezado_fin
FIN_ENCABEZADO

PIE
   pie_estandar
FIN_PIE

FORMATO_FECHA := 3
COLUMNAS      := 80
RENGLONES     := 65

#Variables Globales
VAR_T ( &subtotales )
VAR_T ( &grafica )
VAR_T ( &indices )
VAR_T ( &tot_gen )
_num_ind := 0
$indice := ''
$ant_trab := ''
_dur := 0
_columna_totales := 0
_faux := 0
_fini := 0
_ffin := 0
$saux := ''
$empleado_actual := ''
_nren := 1
_ncol := 1
_tdur := 0

#Subrutina diversas

SUB_RUTINA imprime_empleado
   IMP ( COL(1), 'EMPPRIN':'CLAVE', COL(10), 'EMPPRIN':'NOMBRE' ) ;; IMPRIME
  _ncol := 1
  _nren := _nren + 1
  SI ( $salida = 'EXCEL' )
    EXCEL ( 'PON_VALOR', _ncol, _nren, SUBSTR('EMPPRIN':'CLAVE', 1, 10 ) )
    _ncol := _ncol + 1
    EXCEL ( 'PON_VALOR', _ncol, _nren, SUBSTR('EMPPRIN':'NOMBRE', 1, 30 ) )
    _ncol := _ncol + 1
  FIN_SI
FIN_SUB_RUTINA

SUB_RUTINA sub_totales
   IMPRIME
   DECIMALES := 0
   _num_ind := 1
   MIENTRAS ( TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ) <> '' )
      SI ( VALOR_TABLA ( &tot_gen, $indice, TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind )) <> 0 )
         IMP ( COL ( 10 ), TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ), COL( _columna_totales), DER( .(VALOR_TABLA ( &tot_gen, $indice, TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ))), 3))
         IMPRIME
      FIN_SI
      _num_ind := _num_ind + 1
   FIN_MIENTRAS
   IMP ( COL ( _columna_totales - 8 ), '__________' ) IMPRIME
   IMP ( COL(13), 'DIAS DE INCAPACIDAD EN  ' + $indice + ' : ', COL( _columna_totales), DER( .( VALOR_TABLA( &subtotales, 'SUBTOTAL', $indice ) ), 3 ) ) ;; IMPRIME  
   ASIGNA_VALOR_TABLA( &subtotales, 'SUBTOTAL', $indice, 0 )
   _num_ind := 1
   MIENTRAS ( TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ) <> '' )
      ASIGNA_VALOR_TABLA ( &tot_gen, $indice, TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ), 0 )
     _num_ind := _num_ind + 1
   FIN_MIENTRAS
FIN_SUB_RUTINA

SUB_RUTINA formato_trabajador
    imprime_empleado

    _fini := 'EMPPRIN':'FECHA'
    _ffin := 'EMPPRIN':'FECHA' + 'EMPPRIN':'DURACION' - 1
    SI ( _fini < _fi ) ;; _fini := _fi ;; FIN_SI
    SI ( _ffin > _ff ) ;; _ffin := _ff ;; FIN_SI
    _dur := _ffin - _fini + 1

    #Incremeto de valores para los indices seleccionados
    _num_ind := 0
    REPITE
      INCREMENTA_TABLA( &subtotales, 'SUBTOTAL', TRAE_VALOR_TABLA( &indices, 0, _num_ind ), _dur )
      _num_ind := _num_ind + 1
    HASTA ( TRAE_VALOR_TABLA( &indices, 0, _num_ind ) = '' )

    SI ( _graficar = 1 )
      INCREMENTA_TABLA( &grafica, 'EMPPRIN':'TIPO', 'SUBTOTAL', _dur )
    FIN_SI
    DECIMALES := 0
    IMP ( COL(10), FECHA('EMPPRIN':'FECHA'), COL(23), FECHA('EMPPRIN':'FECHA' + 'EMPPRIN':'DURACION' - 1), COL ( 36 ), 'EMPPRIN':'NUMERO', COL (46), DER ( .('EMPPRIN':'DURACION'), 3))
    IMP ( COL(50), DER( .( _dur ), 3), COL(57), DER( 'EMPPRIN':'TIPO', 4 ), COL ( 65 ), 'EMPPRIN':'CASO' )
    IMPRIME
    SI ( $salida = 'EXCEL' )
      SI ( 'EMPPRIN':'CLAVE' = $empleado_actual )
        _nren := _nren + 1
        _ncol := 3
      FIN_SI
      EXCEL ( 'PON_VALOR', _ncol, _nren, SUBSTR( FECHA('EMPPRIN':'FECHA'), 1, 12 ) )
      _ncol := _ncol + 1
      EXCEL ( 'PON_VALOR', _ncol, _nren, SUBSTR( FECHA('EMPPRIN':'FECHA' + 'EMPPRIN':'DURACION' - 1), 1, 12 ) )
      _ncol := _ncol + 1
      DECIMALES := 0
      EXCEL ( 'PON_VALOR', _ncol, _nren, 'EMPPRIN':'NUMERO' )
      _ncol := _ncol + 1
      EXCEL ( 'PON_VALOR', _ncol, _nren, DER( .('EMPPRIN':'DURACION'), 3) )
      _ncol := _ncol + 1
      EXCEL ( 'PON_VALOR', _ncol, _nren, DER( .( _dur ), 3) )
      _ncol := _ncol + 1
      EXCEL ( 'PON_VALOR', _ncol, _nren, DER( 'EMPPRIN':'TIPO', 4 ))
      _ncol := _ncol + 1
      EXCEL ( 'PON_VALOR', _ncol, _nren, 'EMPPRIN':'CASO' )
    FIN_SI
    $empleado_actual := 'EMPPRIN':'CLAVE'
FIN_SUB_RUTINA

SUB_RUTINA formato_trabajador
   SI ( $ant_trab <> 'EMPPRIN':'CLAVE' )
     imprime_empleado
     _tdur := 0
     $ant_trab := 'EMPPRIN':'CLAVE'
   FIN_SI
   DECIMALES := 0
   _fini := 'EMPPRIN':'FECHAI'
   _ffin := 'EMPPRIN':'FECHAF'
   SI ( _fini < _fi ) ;; _fini := _fi ;; FIN_SI
   SI ( _ffin > _ff ) ;; _ffin := _ff ;; FIN_SI
   _dur := _ffin - _fini + 1

   #Incremeto de valores para los indices seleccionados
   _num_ind := 0
   REPITE
     INCREMENTA_TABLA( &subtotales, 'SUBTOTAL', TRAE_VALOR_TABLA( &indices, 0, _num_ind ), _dur )
     INCREMENTA_TABLA( &tot_gen   , TRAE_VALOR_TABLA( &indices, 0, _num_ind ), 'EMPPRIN':'TIPO', _dur )
     _num_ind := _num_ind + 1
   HASTA ( TRAE_VALOR_TABLA( &indices, 0, _num_ind ) = '' )

   SI ( _graficar = 1 )
     INCREMENTA_TABLA( &grafica, 'EMPPRIN':'TIPO', 'SUBTOTAL', _dur )
   FIN_SI
   IMP ( COL(10), FECHA(_fini), COL(27), FECHA(_ffin), COL(40), DER( .( _dur ), 8), COL(53), 'EMPPRIN':'TIPO' ) ;; IMPRIME
FIN_SUB_RUTINA

INCLUYE UTIL2.DAT

lee_parametros_seleccion
lee_parametros_indices

SI ( $salida = 'EXCEL')
  EXCEL ( 'PON_VALOR', 1, 1, 'CLAVE' )
  EXCEL ( 'PON_VALOR', 2, 1, 'NOMBRE' )
  EXCEL ( 'PON_VALOR', 3, 1, 'F. INICIO' )
  EXCEL ( 'PON_VALOR', 4, 1, 'F. TERMINO' )
  EXCEL ( 'PON_VALOR', 5, 1, 'NUMERO' )
  EXCEL ( 'PON_VALOR', 6, 1, 'TOTAL' )
  EXCEL ( 'PON_VALOR', 7, 1, 'EN PER.' )
  EXCEL ( 'PON_VALOR', 8, 1, 'TIPO' )
FIN_SI

$ind_act := ''
$nom_ind := ''

#Definicion de la tabla de indices utilizados
REPITE
  $nom_ind := TRAE_VALOR_TABLA( &in, 2, _num_ind )
  $ind_act := TRAE_VALOR_TABLA( &in, 0, _num_ind )
  SI (( $nom_ind <> '' ) AND ( $ind_act = '1' ))
    AGREGA_VALOR_COL( &indices, 0, $nom_ind )
  FIN_SI
  _num_ind := _num_ind + 1
HASTA(  $nom_ind = '' )
AGREGA_VALOR_COL( &indices, 0, 'TOTAL' )

$incap := ''
SI ( _eg ) $incap := $incap + '"EG",' FIN_SI
SI ( _rt ) $incap := $incap + '"RT",' FIN_SI
SI ( _at ) $incap := $incap + '"AT",' FIN_SI
SI ( _pm ) $incap := $incap + '"PM",' FIN_SI
SI ( _me ) $incap := $incap + '"ME",' FIN_SI
SI ( _om ) $incap := $incap + '"OM",' FIN_SI
SI ( _eg + _rt + _at + _pm + _me + _om = 6 )
  $incap := '*'
FIN_SI
$incap := ELIMINA ( $incap, LONGITUD ( $incap ), 1 )
SI ( $incap = '' )
   $incap := '*'
FIN_SI

$campos_extras := 'I. FECHA, I. DURACION, I. NUMERO, I. TIPO, I. NUMERO, I. CASO '
$bases_extras := 'EMPINC I'
SI ( SQL_SERVER )
  $condiciones_extras := 'I.CLAVE = E.CLAVE AND I. FECHA <= :FFIN AND DATEADD( DAY, I. DURACION - 1, I. FECHA ) >= :FINI'
SI_NO
  $condiciones_extras := 'I.CLAVE = E.CLAVE AND I. FECHA <= :FFIN'
FIN_SI
SI ( $incap <> '*' )
  $condiciones_extras := $condiciones_extras + ' AND I. TIPO IN ( ' + $incap + ' )'
FIN_SI

base_trabajadores

SI ( SQL_SERVER )
  PARAMETRO_SQL( 'EMPPRIN', 'FINI', _fi )
FIN_SI
PARAMETRO_SQL( 'EMPPRIN', 'FFIN', _ff )

LIMPIA_TABLA( &subtotales )

USA_ARCHIVO ( 'EMPPRIN', 'EMPSDO', 'CLAVE' )

MUESTRA_AVANCE( 'EMPPRIN', 'PROGRESO...' )
PRIMER_REGISTRO( 'EMPPRIN' )
MIENTRAS ( FIN_BASE ( 'EMPPRIN' ) = FALSO )
   SI ( 'EMPPRIN':'FECHA' + 'EMPPRIN':'DURACION' - 1 >= _fi )
     datos_trabajador
   FIN_SI
   SIGUIENTE_REGISTRO ( 'EMPPRIN' )
FIN_MIENTRAS
termina_reporte

IMPRIME IMPRIME

_num_ind := 1
MIENTRAS ( TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ) <> '' )
   DECIMALES := 0
   IMP ( COL ( 10 ), TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ), COL( _columna_totales),  DER( .(VALOR_TABLA ( &tot_gen, 'TOTAL', TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ))), 3 ))
   IMPRIME
   _num_ind := _num_ind + 1
FIN_MIENTRAS

DECIMALES := 0
IMP ( COL ( _columna_totales - 8), '__________' ) IMPRIME
IMP ( COL(13), 'DIAS DE INCAPACIDAD GLOBALES : ', COL( _columna_totales), $( VALOR_TABLA( &subtotales, 'SUBTOTAL', 'TOTAL' ))) ;; IMPRIME  
SI ( _graficar = 1 )
  CREA_GRAFICA( &grafica )
FIN_SI
