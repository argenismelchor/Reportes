#REPORTE DE RETARDOS
#NO EDITAR
#USUARIO =*
#VERSION 2.500
#DESCRIPCION
#Este reporte muestra los retardos de los trabajadores, pide el registro patronal, el indice de ordenamiento, el tipo de falta a reportar y el rango de fechas de las faltas.
#Este reporte imprime de los trabajadores la clave, nombre, fecha ingreso, afiliacion. De las faltas imprime la fecha de inicio, la fecha de termino y el tipo de falta.
#Este reporte permite generar grafica estadistica.
#FIN_DESCRIPCION

NUEVO_PARAMETRO_SAL ( 'SALIDA', 'PANTALLA/IMPRESORA/DISCO', $salida )
NUEVO_PARAMETRO_FEC ( 'FECHA INICIAL' , '', _fi )
NUEVO_PARAMETRO_FEC ( 'FECHA FINAL  ' , '', _ff )
NUEVO_PARAMETRO_CHEQ( 'VIGENTES', '', _vig )
NUEVO_PARAMETRO_SEL ( 'DESCONTAR'     , 'AMBOS/SI/NO', _descontar )
LEE_PARAMETROS

BORRA_PARAMETROS

ENCABEZADO
   IMPRIME
   LETRA ( 'LETRA ENFATISADA' )
   IMP ( 'Rep. ', CLAVE_REPORTE, ' ', NOMBRE_REPORTE, COL (60), VERSION_GIRO ) ;; IMPRIME
   IMP ( CENTRA ('EMPRESA':'NOMBRE')) ;; IMPRIME
   IMP ( CENTRA ('RETARDOS DEL '+ FECHA (_fi)+ ' AL ' + FECHA (_ff))) ;; IMPRIME
   IMP ( REPITETXT('=',80)) ;; IMPRIME
   IMP ( COL(1), 'CLAVE', COL(10), 'NOMBRE' ) ;; IMPRIME
   IMP ( COL(15), 'FECHA', COL(40), 'MINUTOS', COL ( 50 ), 'DESCONTAR' ) ;; IMPRIME
   IMP ( REPITETXT('-',80) );; IMPRIME ;; IMPRIME
   LETRA ( 'LETRA NORMAL' );; IMPRIME
FIN_ENCABEZADO

PIE
   IMPRIME
   DECIMALES := 0
   LETRA ( 'LETRA ENFATISADA' )
   IMPRIME
   IMP ( COL(2), 'Fecha : ', FECHA (FECHA_HOY), ' ', 'Hora : ', HORA(HORA_ACTUAL), COL(65), 'PAGINA -', PAGINA, '-' );; IMPRIME
   LETRA ( 'LETRA NORMAL' );; IMPRIME
   DECIMALES := 2
FIN_PIE

FORMATO_FECHA := 2
SALIDA := $salida

#Variables Globales
VAR_T ( &subtotales )
VAR_T ( &grafica )
VAR_T ( &indices )
_num_ind := 0
$indice := ''
$ant_trab := ''
_dur := 0
_columna_totales := 0
_fini := 0
_ffin := 0

#Subrutina diversas

SUB_RUTINA imprime_empleado
   SI ( $ant_trab <> 'EMPPRIN':'CLAVE' )
     IMP ( COL(1), 'EMPPRIN':'CLAVE', COL(10), 'EMPPRIN':'NOMBRE' ) ;; IMPRIME
     $ant_trab := 'EMPPRIN':'CLAVE'
   FIN_SI
FIN_SUB_RUTINA

SUB_RUTINA sub_totales
  DECIMALES := 0
  IMP ( COL(13), 'Minutos de Retardo en ' + $indice + ' : ', COL(_columna_totales), .( VALOR_TABLA( &subtotales, 'SUBTOTAL', $indice ) ) ) ;; IMPRIME  
  ASIGNA_VALOR_TABLA( &subtotales, 'SUBTOTAL', $indice, 0 )
FIN_SUB_RUTINA

SUB_RUTINA formato_trabajador
   imprime_empleado

   DECIMALES := 0
   _dur := 'EMPPRIN':'RETARDO'

   #Incremeto de valores para los indices seleccionados
   _num_ind := 0
   REPITE
      INCREMENTA_TABLA( &subtotales, 'SUBTOTAL', TRAE_VALOR_TABLA( &indices, 0, _num_ind ), _dur )
      _num_ind := _num_ind + 1
   HASTA ( TRAE_VALOR_TABLA( &indices, 0, _num_ind ) = '' )

   IMP ( COL(15), FECHA('EMPPRIN':'FECHA'), COL(40), DER( .( _dur ), 8), COL ( 55 ), 'EMPPRIN':'DESCONTAR' ) ;; IMPRIME
FIN_SUB_RUTINA

INCLUYE UTIL2.DAT

lee_parametros_seleccion
lee_parametros_indices

$ind_act := ''
$nom_ind := ''

#Definicion de la tabla de indices utilizados
REPITE
  $nom_ind := TRAE_VALOR_TABLA( &in, 2, _num_ind )
  $ind_act := TRAE_VALOR_TABLA( &in, 0, _num_ind )
  SI (( $nom_ind <> '' ) AND ( $ind_act = '1' ))
    AGREGA_VALOR_COL( &indices, 0, $nom_ind )
  FIN_SI
  _num_ind := _num_ind + 1
HASTA(  $nom_ind = '' )
AGREGA_VALOR_COL( &indices, 0, 'TOTAL' )

$campos_extras := 'E. AFILIACION, R. FECHA, R. RETARDO, R. DESCONTAR'
$bases_extras := 'EMPRET R'
$condiciones_extras := 'R.CLAVE = E.CLAVE AND R. FECHA >= :FINI AND R. FECHA <= :FFIN'
SI ( _descontar = 1 )
   $condiciones_extras := $condiciones_extras + ' AND R. DESCONTAR = "S"'
FIN_SI
SI ( _descontar = 2 )
   $condiciones_extras := $condiciones_extras + ' AND R. DESCONTAR = "N"'
FIN_SI
base_trabajadores
PARAMETRO_SQL( 'EMPPRIN', 'FINI', _fi )
PARAMETRO_SQL( 'EMPPRIN', 'FFIN', _ff )

LIMPIA_TABLA( &subtotales )

USA_ARCHIVO ( 'EMPPRIN', 'EMPSDO', 'CLAVE' )

MUESTRA_AVANCE( 'EMPPRIN', 'PROGRESO...' )
PRIMER_REGISTRO( 'EMPPRIN' )
MIENTRAS ( FIN_BASE ( 'EMPPRIN' ) = FALSO )
    SI (( _vig = 0 ) OR ( ES_VIGENTE( _ff ) = VERDADERO ) )
       datos_trabajador
    FIN_SI
    SIGUIENTE_REGISTRO ( 'EMPPRIN' )
FIN_MIENTRAS
termina_reporte

DECIMALES := 0
IMP ( COL(13), 'Minutos de Retardo Globales : ', .( VALOR_TABLA( &subtotales, 'SUBTOTAL', 'TOTAL' ) ) ) ;; IMPRIME  
