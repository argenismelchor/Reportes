#INDICE DE ROTACION
#NO EDITAR
#USUARIO =*
#VERSION 2.500
#DESCRIPCION
#Este reporte muestra el indice de rotación de los empleados de la empresa.
#Este reporte imprime el indice de rotación, el numero de bajas de acuerdo al indice seleccionado.
#Este reporte tiene los filtros estandares los cuales pueden filtrar cualquiera de los indices de Registro patronal, Sucursal
#Centro de Costo, Departamento, Puesto, Tipo Empleado y Tipo de Nómina.
#FIN_DESCRIPCION

NUEVO_PARAMETRO_SAL ( 'SALIDA' , 'PANTALLA/IMPRESORA/DISCO' , $salida )
NUEVO_PARAMETRO_FEC ( 'FECHA INICIAL' , '' , _fi ) 
NUEVO_PARAMETRO_FEC ( 'FECHA FINAL' , '' , _ff )
NUEVO_PARAMETRO_TAB ( 'TIPO BAJA' , 'BAJA/CLAVE/BAJAS' , &bajas )
LEE_PARAMETROS
BORRA_PARAMETROS

INCLUYE ENCPIE.DAT

ENCABEZADO
   $titulo_reporte := 'Indice de Rotación del ' + FECHA (_fi) + ' al ' + FECHA (_ff)
   encabezado_ini
FIN_ENCABEZADO

PIE
   pie_estandar
FIN_PIE 

FORMATO_FECHA := 3
RENGLONES     := 65
COLUMNAS      := 80

#Variables Globales
VAR_T ( &indices )
_num_ind  := 0
_tot_vig  := 0
_stot_vig := 0
_tot_baj  := 0
_stot_baj := 0
_indice   := 0
_no_dias  := 0
$tbaja    := TRAE_SELECCION( &bajas )
VAR_T ( &subtotales )
$indice   := ''

#Subrutinas diversas
SUB_RUTINA imprime_indice
   COLOR ('NEGRO')
   _stot_vig := _stot_vig / ( _ff - _fi + 1 )
   DECIMALES := 2
   IMP ( COL (15) , 'Promedio de Trabajadores : ' , DER ( _stot_vig , 8 ) ) ;; IMPRIME
   DECIMALES := 0
   IMP ( COL (15) , 'Total de Bajas           : ' , DER ( _stot_baj , 8 ) ) ;; IMPRIME
   DECIMALES := 4
   _indice   := ( _stot_baj / _stot_vig ) * 100
   DECIMALES := 2
   IMP ( COL (15) , 'Indice de Rotacion       : ' , DER ( _indice , 8 ) ) ;; IMPRIME
   IMPRIME
FIN_SUB_RUTINA

SUB_RUTINA cuenta_bajas
  PRIMER_REGISTRO( 'EMPSDO' )
  MIENTRAS ( FIN_BASE('EMPSDO') = FALSO )
    SI ( ( 'EMPSDO':'TIPO' = 'B' ) AND ( 'EMPSDO':'FECHA' >= _fi ) AND ( 'EMPSDO':'FECHA' <= _ff ) )
      SI (( $tbaja = '' ) OR ( POS( 'EMPSDO':'CAUSA BAJA', $tbaja ) > 0 ))
        #Incremeto de valores para los indices seleccionados
        _num_ind := 0
        REPITE
          INCREMENTA_TABLA( &subtotales, 'BAJAS', TRAE_VALOR_TABLA( &indices, 0, _num_ind ), 1 )
          _num_ind := _num_ind + 1
        HASTA ( TRAE_VALOR_TABLA( &indices, 0, _num_ind ) = '' )
      FIN_SI
    FIN_SI
    SIGUIENTE_REGISTRO('EMPSDO')
  FIN_MIENTRAS
FIN_SUB_RUTINA

SUB_RUTINA sub_totales
  IMP ( COL(15), 'SUBTOTALES '+ $indice ) ;; IMPRIME
  _stot_vig := VALOR_TABLA( &subtotales, 'DVIG', $indice )
  _stot_baj := VALOR_TABLA( &subtotales, 'BAJAS', $indice )
  imprime_indice
  DECIMALES := 0
  ASIGNA_VALOR_TABLA( &subtotales, 'DVIG', $indice, 0 )
  ASIGNA_VALOR_TABLA( &subtotales, 'BAJAS', $indice, 0 )
FIN_SUB_RUTINA

SUB_RUTINA formato_trabajador
  #Incremeto de valores para los indices seleccionados
  _num_ind := 0
  REPITE
    INCREMENTA_TABLA( &subtotales, 'DVIG', TRAE_VALOR_TABLA( &indices, 0, _num_ind ), _no_dias )
    _num_ind := _num_ind + 1
  HASTA ( TRAE_VALOR_TABLA( &indices, 0, _num_ind ) = '' )

  cuenta_bajas
FIN_SUB_RUTINA

INCLUYE UTIL2.DAT

lee_parametros_seleccion
lee_parametros_indices

$ind_act := ''
$nom_ind := ''

#Definicion de la tabla de indices utilizados
REPITE
  $nom_ind := TRAE_VALOR_TABLA( &in, 2, _num_ind )
  $ind_act := TRAE_VALOR_TABLA( &in, 0, _num_ind )
  SI (( $nom_ind <> '' ) AND ( $ind_act = '1' ))
    AGREGA_VALOR_COL( &indices, 0, $nom_ind )
  FIN_SI
  _num_ind := _num_ind + 1
HASTA(  $nom_ind = '' )
AGREGA_VALOR_COL( &indices, 0, 'TOTAL' )

#Definicion de variables en base a los parametros
SALIDA := $salida

base_trabajadores
USA_ARCHIVO ( 'EMPPRIN', 'EMPSDO', 'CLAVE' )

PRIMER_REGISTRO( 'EMPPRIN' )
MIENTRAS ( FIN_BASE('EMPPRIN') = FALSO )
  _no_dias := FUE_VIGENTE ( _fi, _ff )
  SI ( _no_dias > 0 )
    datos_trabajador
  FIN_SI
  SIGUIENTE_REGISTRO('EMPPRIN')
FIN_MIENTRAS
termina_reporte
IMPRIME ;; IMPRIME
_stot_vig := VALOR_TABLA( &subtotales, 'DVIG', 'TOTAL' )
_stot_baj := VALOR_TABLA( &subtotales, 'BAJAS', 'TOTAL' )
imprime_indice
