#TIPO DE AUSENTISMO
#NO EDITAR
#USUARIO =*
#VERSION 2.500
#DESCRIPCION
#Este reporte despliega el catalogo de tipos de ausentismo.
#Este reporte imprime la clave ,la descripcion y si es o no con goce el tipo de falta.
#FIN_DESCRIPCION
NUEVO_PARAMETRO_SAL ( 'SALIDA' , 'PANTALLA/IMPRESORA/DISCO' , $salida )
NUEVO_PARAMETRO_SEL ( 'ORDEN' , 'CLAVE/DESCRIPCION' , $orden )
LEE_PARAMETROS

FORMATO_FECHA := 3
RENGLONES     := 65
COLUMNAS      := 80
SALIDA        := $salida

INCLUYE ENCPIE.DAT

SI ( $salida = 'DISCO' )
   SALIDA_REPORTE ( DIRECTORIO_TRABAJO + '\TABFALTA.TXT' )
FIN_SI

ENCABEZADO
   $titulo_reporte := 'TIPOS DE AUSENTISMO'
   encabezado_ini
   IMP ( COL (1) , 'Clave' , COL (11) , 'DESCRIPCION' , COL (45) , 'GOCE') ;; IMPRIME
   encabezado_fin
FIN_ENCABEZADO

PIE
   pie_estandar
FIN_PIE 

### VARIABLES
VAR_A( $con_goce )
_total := 0

### DEFINICION DE TABLA
VAR_T ( sql )
PON_VALOR_TABLA ( sql, 0, 0, 'SELECT *' )
PON_VALOR_TABLA ( sql, 0, 1, 'FROM FALTA' ) 
SI ($orden='CLAVE')
  PON_VALOR_TABLA ( sql, 0, 2, 'ORDER BY FALTA.CLAVE' ) 
SI_NO
  PON_VALOR_TABLA ( sql, 0, 2, 'ORDER BY FALTA.DESCRIPCION' ) 
FIN_SI
CREA_CONSULTA ( 'FALTAS', sql )
PRIMER_REGISTRO ( 'FALTAS' )

MIENTRAS ( FIN_BASE ('FALTAS') = FALSO )
  SI ('FALTAS':'GOCE'=0)
    $con_goce := 'NO'
  SI_NO
    $con_goce := 'SI'
  FIN_SI
  IMP ( COL(1), 'FALTAS':'CLAVE', COL(11), 'FALTAS':'DESCRIPCION', COL(45), $con_goce);; IMPRIME
  _total := _total + 1
  SIGUIENTE_REGISTRO ( 'FALTAS' )
FIN_MIENTRAS

DECIMALES := 0
IMPRIME
IMPRIME
IMP ( COL (10), 'TOTAL DE TIPOS DE AUSENTISMOS : ', _total ) ;; IMPRIME

