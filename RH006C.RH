#AUSENTISMO
#VERSION 2.500
#USUARIO =*
#NO EDITAR
#GRAFICA
#EXCEL
#DESCRIPCION
#Este reporte muestra los ausentismos de los trabajadores vigentes a un rango de fechas.
#Este reporte imprime de los trabajadores la clave y nombre, de las faltas imprime, la fecha inicio, termino, duración y tipo.
#Este reporte pide la fecha inicial, final, tipo de falta y si se desea gráfica.
#Este reporte tiene los filtros estandares los cuales pueden filtrar cualquiera de los indices de Registro patronal, Sucursal
#Centro de Costo, Departamento, Puesto, Tipo Empleado y Tipo de Nómina.
#Este reporte permite generar grafica estadistica.
#FIN_DESCRIPCION

NUEVO_PARAMETRO_SAL ( 'SALIDA', 'PANTALLA/IMPRESORA/DISCO/EXCEL', $salida )
NUEVO_PARAMETRO_FEC ( 'FECHA INICIAL' , '', _fi )
NUEVO_PARAMETRO_FEC ( 'FECHA FINAL  ' , '', _ff )
NUEVO_PARAMETRO_TAB ( 'TIPO FALTA' , 'FALTA/CLAVE/TIPOS DE AUSENTISMO', &faltas )
NUEVO_PARAMETRO_CHEQ ( 'GRAFICA' , '', _graficar )
LEE_PARAMETROS
BORRA_PARAMETROS

INCLUYE ENCPIE.DAT

ENCABEZADO
   $titulo_reporte := 'Ausentismos del ' + FECHA (_fi) + ' al ' + FECHA (_ff)
   encabezado_ini  
   IMP ( COL(1), 'CLAVE', COL(10), 'NOMBRE' ) ;; IMPRIME
   IMP ( COL(10), 'INICIO', COL(27), 'TERMINO', COL(40), 'DURACION', COL(50), 'TIPO' )
   IMP ( COL(63), 'DESCRIPCION FALTA'  ) ;; IMPRIME
   encabezado_fin
FIN_ENCABEZADO

PIE
   pie_estandar
FIN_PIE

FORMATO_FECHA := 3
RENGLONES     := 65
COLUMNAS      := 80
SALIDA        := $salida

#Variables Globales
VAR_T ( &subtotales )
VAR_T ( &grafica )
VAR_T ( &indices )
VAR_T ( &tot_gen )
_num_ind := 0
$indice := ''
$ant_trab := ''
_dur := 0
_tdur := 0
_columna_totales := 0
_fini := 0
_ffin := 0
_ren_exc := 4



#Subrutina diversas

SUB_RUTINA imprime_empleado
   IMP ( COL (1) , 'EMPPRIN':'CLAVE' , COL (10) , 'EMPPRIN':'NOMBRE' ) ;; IMPRIME
SI ( $salida = 'EXCEL' )
  EXCEL ( 'PON_VALOR' , 1 , _ren_exc , 'EMPPRIN':'CLAVE' )
  EXCEL ( 'PON_VALOR' , 2 , _ren_exc , 'EMPPRIN':'NOMBRE' )
  _ren_exc := _ren_exc + 1
FIN_SI

FIN_SUB_RUTINA

$indice_texto := ''

SUB_RUTINA sub_totales
   IMPRIME
   DECIMALES := 0
   _num_ind := 1
   SI ( $salida = 'EXCEL' ) ;; _ren_exc := _ren_exc + 1 ;; FIN_SI
   MIENTRAS ( TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ) <> '' )
      SI ( TRAE_REGISTRO ( 'FALTA', TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind )))
         #
      FIN_SI
      SI ( VALOR_TABLA ( &tot_gen, $indice, TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind )) <> 0 )
         IMP ( COL ( 10 ), 'FALTA':'CLAVE', '  ', 'FALTA':'DESCRIPCION', COL( _columna_totales), DER( .(VALOR_TABLA ( &tot_gen, $indice, TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ))), 3))
         IMPRIME
           SI ( $salida = 'EXCEL' )             
             DECIMALES := 0
             EXCEL ( 'PON_VALOR' , 2 , _ren_exc , 'FALTA':'CLAVE' )
             EXCEL ( 'PON_VALOR' , 3 , _ren_exc , 'FALTA':'DESCRIPCION' )
             EXCEL ( 'PON_VALOR' , 4 , _ren_exc , VALOR_TABLA ( &tot_gen, 'TOTAL', TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ) ) )
             _ren_exc := _ren_exc + 1
           FIN_SI
      FIN_SI
      _num_ind := _num_ind + 1
   FIN_MIENTRAS
   IMP ( COL ( _columna_totales ), '__________' ) IMPRIME
   IMP ( COL(13), 'DIAS DE AUSENTISMO EN  ' + $indice_texto + ' : ', COL( _columna_totales), DER( .( VALOR_TABLA( &subtotales, 'SUBTOTAL', $indice ) ), 3 ) ) ;; IMPRIME  
     SI ( $salida = 'EXCEL' )
       EXCEL ( 'PON_VALOR' , 2 , _ren_exc , 'DIAS DE AUSENTISMO EN  ' + $indice_texto + ' : ' + .( VALOR_TABLA( &subtotales, 'SUBTOTAL', $indice ) ) )
       _ren_exc := _ren_exc + 2
     FIN_SI
   ASIGNA_VALOR_TABLA( &subtotales, 'SUBTOTAL', $indice, 0 )
   _num_ind := 1
   MIENTRAS ( TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ) <> '' )
      ASIGNA_VALOR_TABLA ( &tot_gen, $indice, TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ), 0 )
     _num_ind := _num_ind + 1
   FIN_MIENTRAS
FIN_SUB_RUTINA



SUB_RUTINA formato_trabajador
   SI ( $ant_trab <> 'EMPPRIN':'CLAVE' )
     imprime_empleado
     _tdur := 0
     $ant_trab := 'EMPPRIN':'CLAVE'
   FIN_SI
   DECIMALES := 0
   _fini := 'EMPPRIN':'FECHAI'
   _ffin := 'EMPPRIN':'FECHAF'
   SI ( _fini < _fi ) ;; _fini := _fi ;; FIN_SI
   SI ( _ffin > _ff ) ;; _ffin := _ff ;; FIN_SI
   _dur := _ffin - _fini + 1

   #Incremeto de valores para los indices seleccionados
   _num_ind := 0
   REPITE
     INCREMENTA_TABLA( &subtotales, 'SUBTOTAL', TRAE_VALOR_TABLA( &indices, 0, _num_ind ), _dur )
     INCREMENTA_TABLA( &tot_gen   , TRAE_VALOR_TABLA( &indices, 0, _num_ind ), 'EMPPRIN':'TIPO', _dur )
     _num_ind := _num_ind + 1
   HASTA ( TRAE_VALOR_TABLA( &indices, 0, _num_ind ) = '' )

   SI ( _graficar = 1 )
     INCREMENTA_TABLA( &grafica, 'EMPPRIN':'TIPO', 'SUBTOTAL', _dur )
   FIN_SI
   IMP ( COL(10), FECHA(_fini), COL(27), FECHA(_ffin), COL(40), DER( .( _dur ), 8), COL(53), 'EMPPRIN':'TIPO' ) 
   SI ( TRAE_REGISTRO ( 'FALTA', 'EMPPRIN':'TIPO' )) ;; FIN_SI
   IMP ( COL(63), 'FALTA':'DESCRIPCION' );; IMPRIME

   SI ( $salida = 'EXCEL' )
     EXCEL ( 'PON_VALOR' , 2 , _ren_exc , FECHA ( _fini ) )
     EXCEL ( 'PON_VALOR' , 3 , _ren_exc , FECHA ( _ffin ) )
     EXCEL ( 'PON_VALOR' , 4 , _ren_exc , _dur  )
     EXCEL ( 'PON_VALOR' , 5 , _ren_exc , 'EMPPRIN':'TIPO'  )
     _ren_exc := _ren_exc + 1
   FIN_SI
FIN_SUB_RUTINA

INCLUYE UTIL2.DAT

lee_parametros_seleccion
lee_parametros_indices

$ind_act := ''
$nom_ind := ''

#Definicion de la tabla de indices utilizados
REPITE
  $nom_ind := TRAE_VALOR_TABLA( &in, 2, _num_ind )
  $ind_act := TRAE_VALOR_TABLA( &in, 0, _num_ind )
  SI (( $nom_ind <> '' ) AND ( $ind_act = '1' ))
    AGREGA_VALOR_COL( &indices, 0, $nom_ind )
  FIN_SI
  _num_ind := _num_ind + 1
HASTA(  $nom_ind = '' )
AGREGA_VALOR_COL( &indices, 0, 'TOTAL' )

$faltas := TRAE_SELECCION ( &faltas )
$campos_extras := 'E. AFILIACION, A. FECHAI, A. FECHAF, A. TIPO'
$bases_extras := 'EMPAUS A'
$condiciones_extras := 'A.CLAVE = E.CLAVE AND A. FECHAI <= :FF AND A. FECHAF >= :FI'
SI ( $faltas <> '' )
  $condiciones_extras := $condiciones_extras + ' AND A. TIPO IN ( ' + $faltas + ' )'
FIN_SI
_abrir_base := 1
base_trabajadores
PARAMETRO_SQL( 'EMPPRIN', 'FF', _ff )
PARAMETRO_SQL( 'EMPPRIN', 'FI', _fi )

LIMPIA_TABLA( &subtotales )

USA_ARCHIVO ( 'EMPPRIN', 'EMPSDO', 'CLAVE' )

SI ( $salida = 'EXCEL' )
   EXCEL ( 'PON_VALOR' , 1 , 1 , 'CLAVE' )
   EXCEL ( 'PON_VALOR' , 2 , 1 , 'NOMBRE' )
   EXCEL ( 'PON_VALOR' , 2 , 2 , 'INICIO' )
   EXCEL ( 'PON_VALOR' , 3 , 2 , 'TERMINO' )
   EXCEL ( 'PON_VALOR' , 4 , 2 , 'DURACION' )
   EXCEL ( 'PON_VALOR' , 5 , 2 , 'TIPO' )
FIN_SI


MUESTRA_AVANCE( 'EMPPRIN', 'PROGRESO...' )
PRIMER_REGISTRO( 'EMPPRIN' )
MIENTRAS ( FIN_BASE ( 'EMPPRIN' ) = FALSO )
   datos_trabajador
   SIGUIENTE_REGISTRO ( 'EMPPRIN' )
FIN_MIENTRAS
termina_reporte

IMPRIME IMPRIME

_num_ind := 1
MIENTRAS ( TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ) <> '' )
   SI ( TRAE_REGISTRO ( 'FALTA', TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind )))
      #
   FIN_SI
   DECIMALES := 0
   IMP ( COL ( 10 ), 'FALTA':'CLAVE', '  ', 'FALTA':'DESCRIPCION', COL( _columna_totales),  DER( .(VALOR_TABLA ( &tot_gen, 'TOTAL', TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ))), 3 ))
   IMPRIME
     SI ( $salida = 'EXCEL' )
       _ren_exc := _ren_exc + 1      
       EXCEL ( 'PON_VALOR' , 2 , _ren_exc + 2 , 'FALTA':'CLAVE' )
       EXCEL ( 'PON_VALOR' , 3 , _ren_exc + 2 , 'FALTA':'DESCRIPCION' )
       EXCEL ( 'PON_VALOR' , 4 , _ren_exc + 2 , VALOR_TABLA ( &tot_gen, 'TOTAL', TRAE_VALOR_TABLA ( &tot_gen, 0, _num_ind ) ) )
       _ren_exc := _ren_exc + 1
     FIN_SI

   _num_ind := _num_ind + 1
FIN_MIENTRAS

DECIMALES := 0
IMP ( COL ( _columna_totales ), '__________' ) IMPRIME
IMP ( COL(13), 'DIAS DE AUSENTISMO GLOBALES : ', COL( _columna_totales + 2), .( VALOR_TABLA( &subtotales, 'SUBTOTAL', 'TOTAL' ))) ;; IMPRIME  
  SI ( $salida = 'EXCEL' )
    EXCEL ( 'PON_VALOR' , 2 , _ren_exc + 1 , 'DIAS DE AUSENTISMO GLOBALES : ' + .( VALOR_TABLA( &subtotales, 'SUBTOTAL', 'TOTAL' )) )
  FIN_SI

SI ( _graficar = 1 )
  CREA_GRAFICA( &grafica )
  TITULOS_GRAFICA ( &grafica, 'AUSENTISMO', '', '', '' )
FIN_SI
