#TRABAJADORES NO VIGENTES CON FOTO
#NO EDITAR
#USUARIO =*
#VERSION 2.500
#DESCRIPCION
#Este reporte muestra los trabajadores no vigentes de la empresa.
#Este reporte pide fecha inicial y final y permite mostrar la foto del empleado.
#Este reporte imprime de los trabajadores la clave, nombre, departamento, puesto y sucursal.
#Este reporte tiene los filtros estandares los cuales pueden filtrar cualquiera de los indices de Registro patronal, Sucursal
#Centro de Costo, Departamento, Puesto, Tipo Empleado y Tipo de Nómina.
#FIN_DESCRIPCION

NUEVO_PARAMETRO_SAL  ( 'SALIDA' , 'PANTALLA/IMPRESORA/DISCO' , $salida )
NUEVO_PARAMETRO_FEC  ( 'FECHA INICIAL' , '' , _fi ) 
NUEVO_PARAMETRO_FEC  ( 'FECHA FINAL' , '' , _ff )
NUEVO_PARAMETRO_CHEQ ( 'FOTOGRAFIA' , '' , _fotografia )
LEE_PARAMETROS
BORRA_PARAMETROS

VAR_N (_espacio)
MODO_IMPRESION ( 'GRAFICO' )
TAMAÑO_HOJA( 2700, 2100 )
_numero := 0

INCLUYE ENCPIE.DAT

ENCABEZADO
   $titulo_reporte := 'Trabajadores con baja Del ' + FECHA (_fi) + ' al ' + FECHA (_ff)
   encabezado_ini
   SI ( _espacio = 0 )
      IMP ( COL (1) , 'FOTO' ) ;; IMPRIME
   FIN_SI
   IMP ( COL (9-_espacio) , 'CLAVE' , COL (18-_espacio) , 'NOMBRE' )
   IMP ( COL (50) , 'INGRESO', COL (65) , 'BAJA', COL (75) , 'FIRMA' ) ;; IMPRIME
   encabezado_fin
FIN_ENCABEZADO

PIE
   pie_estandar
FIN_PIE

FORMATO_FECHA := 3
RENGLONES     := 65
COLUMNAS      := 90
_y            := 0

#Variables Globales

#Subrutina diversas

SUB_RUTINA formato_trabajador
  _numero := _numero + 1
  IMP ( COL (9-_espacio) , 'EMPPRIN':'CLAVE' , COL (18-_espacio) , SUBSTR ( 'EMPPRIN':'NOMBRE' , 1 , 30 ) )
  IMP ( COL (50) , FECHA( 'EMPPRIN':'INGRESO' ) )
  TRAE_MOV_FECHA( _ff )
  SI ( 'EMPSDO':'TIPO' = 'B' )
    IMP ( COL (65) , FECHA( 'EMPSDO':'FECHA' ) )
  FIN_SI
  IMPRIME
  SI ( _fotografia = 1 )
     _y  := Y_ACTUAL + 260
     VE_Y ( Y_ACTUAL + 100 )
     IMP ( COL (75) , '_____________' )
     IMPRIME
     IMP ( COL (75) , '   Firma' )
     IMPRIME_SIN_SALTO
     SI ( 'EMPPRIN':'FOTO' <> '' )
       SI ( EXISTE_ARCHIVO( 'EMPRESA':'PATH FOTOS' + '\' + 'EMPPRIN':'FOTO' ) )
          IMPRIME_IMAGEN ( 'EMPPRIN':'FOTO' , 40 , Y_ACTUAL - 120 , 160 , 190 )
       FIN_SI
     FIN_SI    

     VE_Y ( _y )
     SI ( _numero >= 7 )
       AVANZA_HOJA
       _numero := 0
     FIN_SI
  FIN_SI
FIN_SUB_RUTINA

INCLUYE UTIL2.DAT

lee_parametros_seleccion
lee_parametros_indices

SALIDA := $salida
SI ( _fotografia = 0 )
   _espacio := 14
SI_NO
   _espacio := 0
FIN_SI

_incluir_suc := 1
_incluir_dep := 1
_incluir_pue := 1
$campos_extras := 'E. FOTO'
base_trabajadores
USA_ARCHIVO( 'EMPPRIN', 'EMPSDO', 'CLAVE' )

MIENTRAS ( FIN_BASE ( 'EMPPRIN' ) = FALSO )
   SI ( ES_VIGENTE ( _ff ) = FALSO )
      SI ( FUE_VIGENTE( _fi, _ff ) )
        datos_trabajador
      FIN_SI
   FIN_SI
   SIGUIENTE_REGISTRO ( 'EMPPRIN' )
FIN_MIENTRAS
termina_reporte
