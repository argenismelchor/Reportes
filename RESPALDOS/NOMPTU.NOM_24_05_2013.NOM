#CALCULO PTU ESTANDAR
#NO EDITAR
#EXCEL

$empresa := LEE_INI( 'PTU', 'ORIGEN', '' )
NUEVO_PARAMETRO_SAL   ( 'SALIDA' , 'PANTALLA/EXCEL' , $salida )
NUEVO_PARAMETRO_TAB   ( 'TIPO DE NOMINA' , 'TIPONOM/CLAVE/TIPOS DE NOMINA' , $tipo )
#NUEVO_PARAMETRO       ( 'EMPRESA ORIGEN', $empresa, $empresa )
NUEVO_PARAMETRO_CHEQ  ( 'GRABAR EN NOMINA' , '' , _afectar )
NUEVO_PARAMETRO_CHEQ ( 'NOCALCIMP' , '' , _nocalcimp ) 
LEE_PARAMETROS
BORRA_PARAMETROS

ESCRIBE_INI( 'PTU', 'ORIGEN', $empresa )
$dir_origen := ''
$actual := 'EMPRESA':'CLAVE'
SI ( $empresa = '' )
  MENSAJE( 'DEBE SELECCIONAR LA EMPRESA ORIGEN.' )
  TERMINA_REPORTE
SI_NO
  SI ( TRAE_REGISTRO( 'EMPRESA', $empresa ) = FALSO )
    MENSAJE( 'LA EMPRESA ORIGEN NO EXISTE.')
  SI_NO
    $dir_origen := 'EMPRESA':'PATH TRABAJO'
  FIN_SI
  SI ( TRAE_REGISTRO( 'EMPRESA', $actual ) )
    #
  FIN_SI
FIN_SI

_subtotal:=0

$tblacum := $dir_origen + '\EMPACUM'
$tipo_nomina := $tipo
# declaracion de variables para calculo
VAR_N ( _faltas )          ;; VAR_N ( _incapacidades )    ;; VAR_N ( _exento )
VAR_N ( _aux1   )          ;; VAR_N ( _base_gravable )    ;; VAR_N ( _aux2 )
VAR_N ( _calculo_ispt )    ;; VAR_N ( _total_sdo )        ;; VAR_N ( _ajuste ) 
VAR_N ( _tipo_nomina )     ;; VAR_N ( _tot_emp )          ;; VAR_A ( $falt )
VAR_N ( _minimo )          ;; VAR_A ( $mes )              ;; VAR_N ( _ptu )
VAR_N ( _sdo    )          ;; VAR_N ( _tot_sdo)           ;; VAR_N ( _ptu_tot)
VAR_N ( _ptu_tot_dias)     ;; VAR_N ( _ptu_tot_sdo)       ;; VAR_N ( _total_dias )
VAR_N ( _dias )            ;; VAR_N ( _salario )          ;; VAR_N ( _aux3 )
VAR_N ( _aux4 )            ;; VAR_N ( _f_dias )           ;; VAR_N ( _f_sdos )
VAR_N ( _acums )           ;; VAR_A ( $conc )             ;; VAR_A ( $saux )
VAR_A ( $suc )             ;; VAR_N ( _fi )               ;; VAR_N ( _ff )
VAR_A ( $ispt )            ;; VAR_A ( $subs )             ;; VAR_A ( $cred )
VAR_N ( _minimo_dias )     ;; VAR_N ( _aux10 )            ;; VAR_N ( _incapacidades2 ) 
VAR_N ( _incapacidades3 ) 


FORMATO_FECHA := 3
FORMATO_PESOS := 2
COLUMNAS      := 120 
_ajuste       := 0.10
_bono         := 0 
_dias_bono    := 0 

ENCABEZADO
   LETRA ( 'LETRA CONDENSADA' )
   IMPRIME
   IMP ( COL (1) , 'CALCULO DE PTU' , COL (108), 'giro Ver 1.0' ) ;; IMPRIME
   IMP ( CENTRA ('EMPRESA':'NOMBRE') ) ;; IMPRIME
   IMP ( CENTRA ( 'Del ' + FECHA ( _fi ) + ' al ' + FECHA ( _ff ) ) ) ;; IMPRIME
   IMP ( REPITETXT ( '=' , 130 ) ) ;; IMPRIME
   IMP ( COL (1)   , 'Clave' , COL (10) , 'Nombre' , COL (42) , DER ( 'DIAS' , 7 ) , COL (50) , DER ( 'BASE PERCEP' , 15 ) )
   IMP ( COL (65)  , DER ( 'PTU Dias' , 12 ) , COL (77) , DER ( 'PTU Sdos' , 12 ) , COL (89) , DER ( 'TOTAL PTU' , 12 ) )
   IMP ( COL (102) , DER ( 'ISPT' , 12 ) , COL (115) , DER ( 'TOTAL' , 15 ) ) ;; IMPRIME
   IMP ( REPITETXT ( '-' , 130 ) ) ;; IMPRIME
   LETRA ( 'LETRA CONDENSADA' )
   IMPRIME
FIN_ENCABEZADO

PIE
   IMPRIME
   DECIMALES := 0
   LETRA ( 'LETRA CONDENSADA' )
   IMP ( COL (1) , 'Fecha : ' , FECHA (FECHA_HOY) , ' ' , 'Hora : ' , HORA (HORA_ACTUAL) , COL (85) , 'PAGINA -' , PAGINA , '-' ) ;; IMPRIME
   LETRA ( 'LETRA CONDENSADA' ) ;; IMPRIME
   DECIMALES := 2
FIN_PIE 

CREA_KARDEX ('TOTAL')

SUB_RUTINA _imp_tot
   DECIMALES := 2
   PRIMER_REGISTRO ( 'NOMINA' ) 
   MIENTRAS ( FIN_BASE ( 'NOMINA' ) = 0 )
      SI ( 'NOMINA':'TOTAL' <> 0 )
         SI  ( TRAE_REGISTRO ( 'CONCEPTO', 'NOMINA':'CLAVE' ) )
            SI ( 'CONCEPTO':'IMPRIMIR' <> 0 )
               IMP ( 'CONCEPTO':'CLAVE', COL (10), 'CONCEPTO':'DESCRIPCION' )
               SI ( 'CONCEPTO':'PDC' = 0 ) ;; COL (40) ;; FIN_SI
               SI ( 'CONCEPTO':'PDC' = 1 ) ;; COL (50) ;; FIN_SI        
               SI ( 'CONCEPTO':'PDC' = 2 ) ;; COL (60) ;; FIN_SI
               IMP ( DER ( $ ( 'NOMINA':'TOTAL' ), 15 ))
               IMPRIME
            FIN_SI
         SI_NO
            IMP ( COL (11), 'DESCONOCIDO' )
            IMP ( DER ( $ ( 'NOMINA':'TOTAL' ), 15 ))
            IMPRIME
         FIN_SI
      FIN_SI
      SIGUIENTE_REGISTRO ( 'NOMINA' )
   FIN_MIENTRAS
FIN_SUB_RUTINA

SI ( TRAE_REGISTRO ( 'TIPONOM', $tipo_nomina ) = 0 )
  IMP ( 'NO SE HA ENCONTRADO EL TIPO DE NOMINA SELECCIONADO' ) ;; IMPRIME
  TERMINA_REPORTE
FIN_SI
SI ( TRAE_REGISTRO ( 'PERIODO', $tipo_nomina, 'TIPONOM':'ACTUAL' ) = 0 )
  IMP ( 'NO SE HA ENCONTRADO EL PERIODO ACTUAL DEL TIPO DE NOMINA SELECCIONADO' ) ;; IMPRIME
  TERMINA_REPORTE
FIN_SI

$mes := 'PERIODO':'MES DE ACUMULACION'

_fi := CALCULA_FECHA (AÑO(FECHA_HOY)-1 , 1 , 1  ) 
_ff := CALCULA_FECHA (AÑO(FECHA_HOY)-1 , 12 , 31  )

FORMATO_FECHA := 3
SI ( TRAE_REGISTRO_VIG ( 'MINIMO', _fi ) = 0 )
  IMP ( 'NO SE HA ENCONTRADO EL SUELDO MINIMO PARA EL PERIODO ACTUAL DEL TIPO DE NOMINA SELECCIONADO' ) ;; IMPRIME
  TERMINA_REPORTE
FIN_SI
SI ( TRAE_REGISTRO_VIG ( 'PAGOIMSS', _fi ) = 0 )
  IMP ( 'NO SE HAN ENCONTRADO LOS PORCENTAJES DE PAGO AL IMSS PARA EL PERIODO ACTUAL DEL TIPO DE NOMINA SELECCIONADO' ) ;; IMPRIME
  TERMINA_REPORTE
FIN_SI
FORMATO_FECHA := 3

NUEVO_PARAMETRO_TAB ('EMPPRIN' , 'EMPPRIN/CLAVE/CLAVE' , &empleado) 
LEE_PARAMETROS
BORRA_PARAMETROS

VAR_T ( &tabla_sql )
AGREGA_VALOR_COL ( &tabla_sql, 0, 'SELECT E.CLAVE, E.NOMBREP || " " || E.NOMBREM || " " || E.NOMBREN AS NOMBRE, E.INGRESO, E.TIPO_NOM, E.CUOTA_SINDICAL, E.PTU, E.PE,' )
AGREGA_VALOR_COL ( &tabla_sql, 0, 'E. "SEMANA REDUCIDA",E. "JORNADA REDUCIDA", E."TIPO DESCUENTO", E."VALOR DESCUENTO", E. CALENDARIO' )
AGREGA_VALOR_COL ( &tabla_sql, 0, 'FROM EMPPRIN E' )
SI ( TRAE_VALOR_TABLA ( &empleado, 0, 0 ) <> '*' )
   AGREGA_VALOR_COL ( &tabla_sql, 0, 'WHERE E.CLAVE IN' )
   CREA_SELECCION_SQL ( &tabla_sql, &empleado )
   AGREGA_VALOR_COL ( &tabla_sql, 0, 'AND E.TIPO_NOM = "' + $tipo + '"' )
 SI_NO
   AGREGA_VALOR_COL ( &tabla_sql, 0, 'WHERE E.TIPO_NOM = "' + $tipo + '"' )
FIN_SI
AGREGA_VALOR_COL ( &tabla_sql, 0, 'ORDER BY E. TIPO_NOM, E.CLAVE' )
CREA_CONSULTA ( 'EMPPRIN', &tabla_sql )

PRIMER_REGISTRO ( 'EMPPRIN' )
USA_ARCHIVO ( 'EMPPRIN' , 'EMPPUES' , 'CLAVE' )
USA_ARCHIVO ( 'EMPPRIN' , 'EMPSDO'  , 'CLAVE' )
USA_ARCHIVO ( 'EMPPRIN' , 'EMPVACA' , 'CLAVE' )
USA_ARCHIVO ( 'EMPPRIN' , 'EMPAUS'  , 'CLAVE' )
USA_ARCHIVO ( 'EMPPRIN' , 'EMPINC'  , 'CLAVE' )
USA_ARCHIVO ( 'EMPPRIN' , $tblacum , 'CLAVE' )


_ren_excel  := 1
SI ( $salida = 'EXCEL' )
  EXCEL( 'PON_VALOR', 1, _ren_excel, 'CLAVE' )
  EXCEL( 'PON_VALOR', 2, _ren_excel, 'NOMBRE' )
  EXCEL( 'PON_VALOR', 3, _ren_excel, 'DIAS' )
  EXCEL( 'PON_VALOR', 4, _ren_excel, 'SUELDO' )
  EXCEL( 'PON_VALOR', 5, _ren_excel, 'PTU TOT DIAS' )
  EXCEL( 'PON_VALOR', 6, _ren_excel, 'PTU TOT SUELDO' )
  EXCEL( 'PON_VALOR', 7, _ren_excel, 'PTU SUMA' )
  EXCEL( 'PON_VALOR', 8, _ren_excel, 'ISPT' )
  EXCEL( 'PON_VALOR', 9, _ren_excel, 'BONO' )
  EXCEL( 'PON_VALOR', 10, _ren_excel, 'TOTAL' )
  _ren_excel := _ren_excel + 1
FIN_SI


# ********** Cálculo por trabajador
USA_ARCHIVO ( 'EMPPRIN' , 'EMPNOM'  , 'CLAVE' ) 
USA_ARCHIVO ( 'EMPPRIN' , 'EMPPUES' , 'CLAVE' ) 


SI ( CAMBIA_PERIODO ('TIPONOM':'CLAVE' , 'TIPONOM':'ACTUAL',0) = FALSO )
 TERMINA_REPORTE 
FIN_SI

MIENTRAS ( FIN_BASE ( 'EMPPRIN' ) = 0 )
   DECIMALES := DECIMALES 
  # PON_TOTAL_0
   _dias := 0
   _dias := FUE_VIGENTE ( _fi , _ff )
   SI ( _dias > 366 )
      _dias := 365
   FIN_SI

   MUESTRA_AVANCE ( 'EMPPRIN' , 'EMPPRIN':'CLAVE' )
   SI ( CAP1 ('31') <> 0 )
      PRIMER_REGISTRO ( 'NOMINA'  )
      PON_TOTAL_0
      INCLUYE _PTU
      DECIMALES := 0
      IMP ( COL (1) , 'EMPPRIN':'CLAVE' , COL (10) , SUBSTR ( 'EMPPRIN':'NOMBRE' , 1 , 30 ) , COL (42) , DER ( _dias , 7 ) )
      DECIMALES := 2
      IMP ( COL (50)  , DER ( $ (_sdo) , 15 ) ) 
      IMP ( COL (65)  , DER ( $ (_ptu_tot_dias) , 12 ) )  
      IMP ( COL (77)  , DER ( $ (_ptu_tot_sdo)  , 12 ) ) 
      IMP ( COL (89)  , DER ( $ (TOTAL('31')) , 12 ) )
      IMP ( COL (102) , DER ( $ (TOTAL ('51'))   , 12 ) )
      IMP ( COL (115) , DER ( $ (TOTAL ('TOTA')) , 15 ) )
      IMPRIME
      SI ( $salida = 'EXCEL' )
        EXCEL( 'PON_VALOR', 1, _ren_excel, 'EMPPRIN':'CLAVE' )
        EXCEL( 'PON_VALOR', 2, _ren_excel, 'EMPPRIN':'NOMBRE' )
        EXCEL( 'PON_VALOR', 3, _ren_excel, .( _dias ) )
        EXCEL( 'PON_VALOR', 4, _ren_excel, $( _sdo ) )
        EXCEL( 'PON_VALOR', 5, _ren_excel, $( _ptu_tot_dias ) )
        EXCEL( 'PON_VALOR', 6, _ren_excel, $( _ptu_tot_sdo ) )
        EXCEL( 'PON_VALOR', 7, _ren_excel, $( TOTAL('31') ) )
        EXCEL( 'PON_VALOR', 8, _ren_excel, $( TOTAL('51') ))
        EXCEL( 'PON_VALOR', 9, _ren_excel, $( TOTAL('26') ) )
        EXCEL( 'PON_VALOR', 10, _ren_excel, $( TOTAL('TOTA') ) )
        _ren_excel := _ren_excel + 1
      FIN_SI
      DECIMALES := 50
      ACUMULA_TOTALES ( 'TOTAL' )
      _dias        := 0
      _sdo         := 0
   FIN_SI
   SI ( _afectar = 1 )
     GRABA_BASE ( 'EMPNOM' )
   FIN_SI
   SIGUIENTE_REGISTRO ( 'EMPPRIN' )
FIN_MIENTRAS

# termino del reporte
AVANZA_HOJA
DECIMALES := 2
IMPRIME ;; IMPRIME 
IMPRIME


IMPRIME
IMPRIME ;; IMPRIME
IMP ( '    **** TOTALES GENERALES ****' ) ;; IMPRIME ;; IMPRIME
USA_KARDEX ('TOTAL')
_imp_tot
IMPRIME ;; IMPRIME
DECIMALES := 0
IMP ( '  *** EMPLEADOS CALCULADOS : ', _tot_emp ) ;; IMPRIME
IMPRIME ;; IMPRIME
IMP ( '  --------------------------------- FIN DE REPORTE ---------------------------' ) ;; IMPRIME
