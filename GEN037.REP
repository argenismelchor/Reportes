#IMPORTACION DE DSP
#NO EDITAR
#USUARIO =*
#VERSION 2.500
#DESCRIPCION
#Importacion datos de una archivo de texto DSP.TXT del directorio de trabajo con el siguiente formato:
#CAMPO       COL LON JUST. FORMATO
#CLAVE         1  10  IZQ
#FECHA        11   8  IZQ DDMMAAAA
#CATALOGO     21  10  IZQ
#FIN          31  1   IZQ #
#FIN_DESCRIPCION

NUEVO_PARAMETRO      ( 'ARCHIVO', DIRECTORIO_TRABAJO + '\DSP.TXT', $archivo )
NUEVO_PARAMETRO_SEL  ( 'TIPO', 'DEPTO/PUESTO/SUCURSAL', $dsp )
LEE_PARAMETROS

FORMATO_FECHA := 2

CREA_BASE_TXT ( 'TABLA', $archivo, 'FIJO' )

DEFINE_CAMPO_TXT ( 'TABLA' , 'CLAVE'   , 1   , 10  )
DEFINE_CAMPO_TXT ( 'TABLA' , 'FECHA'   , 11  , 8   )
DEFINE_CAMPO_TXT ( 'TABLA' , 'CATALOGO', 21  , 10  )
ABRE_BASE_TXT ( 'TABLA', 'EDICION' )

ENCABEZADO
   IMPRIME
   LETRA ( 'LETRA ENFATISADA' )
   IMP ( 'Rep. ', CLAVE_REPORTE, ' ', NOMBRE_REPORTE, COL (60), VERSION_GIRO );; IMPRIME
   IMP ( CENTRA ('EMPRESA':'NOMBRE') ) ;; IMPRIME
   IMP ( CENTRA ('IMPORTACION DE DSP')) ;; IMPRIME
   IMP ( REPITETXT('=',90)) ;; IMPRIME
   IMP ( COL(1), 'CLAVE', COL(10), 'NOMBRE', COL(50), 'FECHA', COL(65), 'CATALOGO' );; IMPRIME
   IMP ( REPITETXT('-',90) );; IMPRIME;; IMPRIME 
   LETRA ( 'LETRA NORMAL' );; IMPRIME
FIN_ENCABEZADO

PIE
   IMPRIME
   DECIMALES := 0
   LETRA ( 'LETRA ENFATISADA' )
   IMPRIME
   IMP ( COL(2), 'Fecha : ', FECHA (FECHA_HOY), ' ', 'Hora : ', HORA(HORA_ACTUAL), COL(65), 'PAGINA -', PAGINA, '-' );; IMPRIME
   LETRA ( 'LETRA NORMAL' );; IMPRIME
   DECIMALES := 2
FIN_PIE

$empdsp := ''
SI ( $dsp = 'DEPTO' )
  $empdsp := 'EMPDEP'
FIN_SI
SI ( $dsp = 'PUESTO' )
  $empdsp := 'EMPPUES'
FIN_SI
SI ( $dsp = 'SUCURSAL' )
  $empdsp := 'EMPSUC'
FIN_SI

ABRE_BASE  ( 'EMPPRIN' )
USA_ARCHIVO( 'EMPPRIN', $empdsp, 'CLAVE' )

# DECLARACION DE VARIABLES  GLOBALES
$clave   := ''
_dia    := 0
_mes    := 0
_año    := 0
_mas    := 0
_act    := 0
_fecha  := 0
$catalogo := ''

PRIMER_REGISTRO ( 'TABLA' )
MIENTRAS ( FIN_BASE ( 'TABLA' ) = FALSO )

#######################################################    ASIGNACION CLAVE

  $clave := QUITA_CARACTER( ' ', 'TABLA':'CLAVE' )
  SI ( TRAE_REGISTRO( 'EMPPRIN', $clave ) )


  #######################################################    FECHA INGRESO ( DDMMAAAA)
  
    _dia := VALOR ( SUBSTR( 'TABLA':'FECHA', 1, 2 ) )
    _mes := VALOR ( SUBSTR( 'TABLA':'FECHA', 3, 2 ) )
    _año := VALOR ( SUBSTR( 'TABLA':'FECHA', 5, 4 ) )
    _fecha := CALCULA_FECHA ( _año, _mes, _dia )
    $catalogo := 'TABLA':'CATALOGO'

    SI ( TRAE_REGISTRO( $dsp, $catalogo ) )
      SI ( TRAE_REGISTRO( $empdsp, $clave, _fecha ) = FALSO )
        AGREGA_REGISTRO( $empdsp )
        $empdsp:'CLAVE' := $clave
        $empdsp:'FECHA ENTRADA' := _fecha
        _mas := _mas + 1
      SI_NO
        _act := _act + 1
      FIN_SI
      $empdsp:'CATALOGO' := $catalogo
      $empdsp:'FECHA SALIDA' := CALCULA_FECHA( 2099, 12, 31 )
      GRABA_BASE ( $empdsp )
      IMP ( COL(1), 'EMPPRIN':'CLAVE', COL(10), 'EMPPRIN':'NOMBREP' + ' ' + 'EMPPRIN':'NOMBREM' + ' ' + 'EMPPRIN':'NOMBREN' )
      IMP ( COL(50), FECHA( _fecha ) )
      IMP ( COL(65), $dsp:'CLAVE' + ' ' + $dsp:'DESCRIPCION' )
      IMPRIME
    SI_NO
      IMP ( $dsp + ' NO ENCONTRADO : ', $catalogo, ' EMPLEADO : ', $clave ) ;; IMPRIME
    FIN_SI
  SI_NO
    IMP ( 'EMPLEADO NO ENCONTRADO : ', $clave ) ;; IMPRIME
  FIN_SI

################################################################

  SIGUIENTE_REGISTRO( 'TABLA' )

FIN_MIENTRAS

IMPRIME ;; IMPRIME
DECIMALES := 0
IMP ( 'REGISTROS AGREGADOS    : ', _mas ) ;; IMPRIME
IMP ( 'REGISTROS ACTUALIZADOS : ', _act ) ;; IMPRIME

TERMINA_REPORTE
