#TODAS LAS INCIDENCIAS DE AUSENTISMO
#VERSION 1.0
#USUARIO =*
#NO EDITAR
#DESCRIPCION
#Reporte que despliega todas las incidencias de ausentismo de los empleados en el rango de fechas seleccionadas.
#FIN_DESCRIPCION

NUEVO_PARAMETRO_SAL ( 'SALIDA', 'PANTALLA/IMPRESORA', $salida)
NUEVO_PARAMETRO_FEC ( 'FECHA INICIAL', '', _fi )
NUEVO_PARAMETRO_FEC ( 'FECHA FINAL'  , '', _ff )

LEE_PARAMETROS
BORRA_PARAMETROS

FORMATO_FECHA := 2
SALIDA := $salida

INCLUYE ENCPIE.DAT

ENCABEZADO   
   $titulo_reporte := 'INCAPACIDADES-FALTAS-RETARDOS DEL ' + FECHA (_fi) + ' AL ' + FECHA (_ff)
   encabezado_ini
   IMP ( COL ( 1 ), 'CLAVE', COL ( 10 ), 'NOMBRE', COL ( 50 ), DER('FALTAS',10), COL ( 60 ), DER('INCS',10), COL (70 ), DER('RETARDO',10) ) IMPRIME
   encabezado_fin
FIN_ENCABEZADO

PIE
   pie_estandar
FIN_PIE 

_inc := 0
_ret := 0
_fal := 0

SUB_RUTINA formato_trabajador
   IMP ( COL ( 1 ), 'EMPPRIN':'CLAVE', COL ( 10 ), 'EMPPRIN':'NOMBRE', COL ( 50 ), DER ( _fal, 10 ), COL ( 60 ), DER ( _inc, 10 ), COL ( 70 ), DER ( _ret, 10 )  )
   IMPRIME
FIN_SUB_RUTINA

INCLUYE UTIL2.DAT

lee_parametros_seleccion
lee_parametros_indices
base_trabajadores

USA_ARCHIVO ( 'EMPPRIN', 'EMPSDO', 'CLAVE' )
USA_ARCHIVO ( 'EMPPRIN', 'EMPINC', 'CLAVE' )
USA_ARCHIVO ( 'EMPPRIN', 'EMPRET', 'CLAVE' )

PRIMER_REGISTRO ( 'EMPPRIN' )
MIENTRAS ( FIN_BASE ( 'EMPPRIN' ) = FALSO )
   SI ( FUE_VIGENTE ( _fi, _ff ))
      _inc := TRAE_INCAPACIDADES ( _fi, _ff, '*' )
      _fal := TOTAL_FALTAS ( _fi, _ff, '*' )
      _ret := TRAE_RETARDOS ( _fi, _ff )
      SI (( _inc + _fal + _ret ) > 0 )
         datos_trabajador
      FIN_SI
   FIN_SI
   SIGUIENTE_REGISTRO ( 'EMPPRIN' )
FIN_MIENTRAS

termina_reporte
