#BORRADO DE PRESTAMOS CON TODO Y ABONOS
#NO EDITAR
#USUARIO =*
#VERSION 2.500
#DESCRIPCION
#Este reporte permite eliminar abonos realizados a prestamos del concepto seleccionado y la fecha seleccionada.
#Favor de Actualizarla.
#FIN_DESCRIPCION

NUEVO_PARAMETRO_SAL ('SALIDA','PANTALLA/IMPRESORA',$salida)
NUEVO_PARAMETRO     ('CONCEPTO','',$conc)
NUEVO_PARAMETRO_SEL ('TIPO', 'TODOS/SALDADOS/PENDIENTES', _tipo )
LEE_PARAMETROS

FORMATO_FECHA := 2
SALIDA := $salida

ENCABEZADO   
   IMPRIME
   LETRA ( 'LETRA ENFATISADA' )
   IMP ( 'Rep. ', CLAVE_REPORTE, ' ', NOMBRE_REPORTE, COL (60), VERSION_GIRO );; IMPRIME
   IMP ( CENTRA ('EMPRESA':'NOMBRE' ));; IMPRIME
   IMP ( CENTRA ('BORRADO DE PRESTAMOS'));; IMPRIME
   IMP ( REPITETXT('=',80));; IMPRIME
   IMP ( 'CLAVE', COL(10), 'NOMBRE' );; IMPRIME
   IMP ( REPITETXT('-',80) );; IMPRIME
   LETRA ( 'LETRA NORMAL' )
   IMPRIME
FIN_ENCABEZADO
PIE
   IMPRIME ;; IMPRIME
   DECIMALES := 0
   LETRA ( 'LETRA ENFATISADA' )
   IMP ( COL(2), 'Fecha : ', FECHA (FECHA_HOY), ' ', 'Hora : ', HORA (HORA_ACTUAL), COL(65), 'PAGINA -', PAGINA, '-' );; IMPRIME
   LETRA ( 'LETRA NORMAL' );; IMPRIME
   DECIMALES := 2
FIN_PIE 

VAR_T ( &tabla )
AGREGA_VALOR_COL( &tabla, 0, 'SELECT *' )
AGREGA_VALOR_COL( &tabla, 0, 'FROM EMPPRES' )
AGREGA_VALOR_COL( &tabla, 0, 'WHERE CLAVE_CONCEPTO = "' + $conc + '"' )
SI ( _tipo = 1 )
  AGREGA_VALOR_COL( &tabla, 0, ' AND SALDO <= 0.01' )
FIN_SI
SI ( _tipo = 2 )
  AGREGA_VALOR_COL( &tabla, 0, ' AND SALDO > 0.01' )
FIN_SI
CREA_CONSULTA( 'EMPPRES1', &tabla )
USA_ARCHIVO( 'EMPPRES1', 'EMPABO', 'ID' )

PRIMER_REGISTRO( 'EMPPRES1' )
MIENTRAS( FIN_BASE( 'EMPPRES1' ) = FALSO )
  SI ( TRAE_REGISTRO( 'EMPPRIN', 'EMPPRES1':'CLAVE' ) )
    IMP ( 'EMPPRES':'CLAVE', COL(10), 'EMPPRIN':'NOMBREP' + ' ' + 'EMPPRIN':'NOMBREM' + ' ' + 'EMPPRIN':'NOMBREN' )
    IMPRIME
  FIN_SI
  PRIMER_REGISTRO( 'EMPABO' )
  MIENTRAS( FIN_BASE( 'EMPABO' ) = FALSO )
    BORRA_REGISTRO( 'EMPABO' )
  FIN_MIENTRAS
  SI ( TRAE_REGISTRO ( 'EMPPRES', 'EMPPRES1':'CLAVE', 'EMPPRES1':'ID' ) )
    BORRA_REGISTRO( 'EMPPRES' )
  FIN_SI
  SIGUIENTE_REGISTRO ( 'EMPPRES1' )
FIN_MIENTRAS

TERMINA_REPORTE
