#RECIBOS NOTIFORMAS DIRECTO
#NO EDITAR
#USUARIO =*
#VERSION 2.605
#DESCRIPCION
#Este reporte imprime los recibos notiforma de los trabajadores que recibieron pago en el perido. 
#Este reporte imprime el formato exacto de los recibos notiforma en forma directa lo que acelera bastante la impresion.
#Este reporte tiene los filtros estandares los cuales pueden filtrar cualquiera de los indices de Registro patronal, Sucursal
#Centro de Costo, Departamento, Puesto, Tipo Empleado y Tipo de Nómina.
#FIN_DESCRIPCION

NUEVO_PARAMETRO_SAL  ( 'SALIDA', 'PANTALLA/IMPRESORA/DISCO', $salida )
NUEVO_PARAMETRO_CHEQ ( 'AJUSTAR', '', _ajustar )
LEE_PARAMETROS

BORRA_PARAMETROS

FORMATO_FECHA := 3
FORMATO_PESOS := 2
_detalle_prestamos := 0   #PERMITE DESLOSAR EL DETALLE DE PRESTAMOS.

_ajuste_x := VALOR( LEE_INI( TRAE_IMPRESORA, 'AJUSTE_X_RECIBOS_DIRECTO', '0' ) )
_ajuste_y := VALOR( LEE_INI( TRAE_IMPRESORA, 'AJUSTE_Y_RECIBOS_DIRECTO', '0' ) )
_inix := 0
_iniy := 0
_total_conceptos := VALOR( LEE_INI( TRAE_IMPRESORA, 'TOTAL_CONCEPTOS_RECIBOS', '12' ) )
SI ( _ajustar = 1 )
    DECIMALES := 0
    _inix := _ajuste_x
    _iniy := _ajuste_y
    NUEVO_PARAMETRO ( 'AJUSTE X', .(_inix), _ajuste_x )
    NUEVO_PARAMETRO ( 'AJUSTE Y', .(_iniy), _ajuste_y )
    NUEVO_PARAMETRO ( 'TOTAL CONCEPTOS', .(_total_conceptos), _total_conceptos )
    LEE_PARAMETROS

    BORRA_PARAMETROS
    #GRABA EL NUEVO VALOR DE AJUSTE EN EL INI
    SI (( _ajuste_x <> _inix ) OR ( _ajuste_y <> _iniy ))
      ESCRIBE_INI( TRAE_IMPRESORA, 'AJUSTE_X_RECIBOS_DIRECTO', .(_ajuste_x) )
      ESCRIBE_INI( TRAE_IMPRESORA, 'AJUSTE_Y_RECIBOS_DIRECTO', .(_ajuste_y) )
    FIN_SI
    ESCRIBE_INI( TRAE_IMPRESORA, 'TOTAL_CONCEPTOS_RECIBOS', .(_total_conceptos) )
FIN_SI

FORMATO_FECHA := 1 
SALIDA        := $salida
RENGLONES     :=30
MODO_IMPRESION ( 'DIRECTO' )
TAMAÑO_HOJA ( 1017, 1900 )
LETRA ( 'LETRA 2' )
#SALIDA_REPORTE( 'SALIDA.TXT' )

### VARIABLES GLOBALES DEL REPORTE
VAR_T ( &kardex)
VAR_N ( _aux1  )
VAR_N ( _perc  )
VAR_N ( _dedu  )
VAR_A ( $c_perc )
VAR_A ( $d_perc )
VAR_N ( _t_perc )
VAR_A ( $c_deduc )
VAR_A ( $d_deduc )
VAR_N ( _t_deduc )
VAR_N ( _num_registro )
VAR_A ( $aux )

#Subrutina diversas

SUB_RUTINA formato_trabajador
  #
FIN_SUB_RUTINA

INCLUYE UTIL2.DAT

SUB_RUTINA trae_renglon
  $c_perc:=TRAE_VALOR_TABLA( &kardex, 0, _num_registro )
  $d_perc:=TRAE_VALOR_TABLA( &kardex, 1, _num_registro )
  $aux:=TRAE_VALOR_TABLA( &kardex, 5, _num_registro )
  _t_perc:=VALOR( $aux )
  $c_deduc:=TRAE_VALOR_TABLA( &kardex, 6, _num_registro )
  $d_deduc:=TRAE_VALOR_TABLA( &kardex, 7, _num_registro )
  $aux:=TRAE_VALOR_TABLA( &kardex, 11, _num_registro )
  _t_deduc:=VALOR( $aux )
FIN_SUB_RUTINA

SUB_RUTINA _enc
   _iniy := 0
   MIENTRAS ( _iniy < _ajuste_y )
     IMPRIME
     _iniy := _iniy + 1
   FIN_MIENTRAS
   IMP ( COL (1 + _ajuste_x), 'EMPPRIN':'CLAVE', COL (8 + _ajuste_x), 'EMPPRIN':'NOMBRE', COL (81 + _ajuste_x), FORMATO ( 'EMPPRIN':'AFILIACION', 'XXX-XX-XXXX-X' ), COL (104 + _ajuste_x), FORMATO ( 'EMPPRIN':'RFC', 'XXXX-XXXXXX-XXX' ) )
   IMPRIME
   IMPRIME
   DECIMALES:=2
   SI ( CD_DC = 0 )
     SI ( TRAE_REGISTRO ( 'DEPTO', TRAE_DSP ( 'D', 'PERIODO':'FECHA FINAL' ) ) ) ;; FIN_SI
     IMP ( COL (8 + _ajuste_x), 'DEPTO':'DESCRIPCION', COL (59 + _ajuste_x), $( SUELDO_ACTUAL ( 'PERIODO':'FECHA INICIAL', 'PERIODO':'FECHA FINAL' ) ) )
   SI_NO
     SI ( TRAE_REGISTRO ( 'CENTROC', TRAE_DSP ( 'D', 'PERIODO':'FECHA FINAL' ) ) ) ;; FIN_SI
     IMP ( COL (8 + _ajuste_x), 'CENTROC':'DESCRIPCION', COL (59 + _ajuste_x), $( SUELDO_ACTUAL ( 'PERIODO':'FECHA INICIAL', 'PERIODO':'FECHA FINAL' ) ) )
   FIN_SI
   DECIMALES:=0
   IMP ( COL(75 + _ajuste_x), 'PERIODO':'NUMERO', COL (81 + _ajuste_x), FECHA ( 'PERIODO':'FECHA FINAL' ) )
   DECIMALES:=0
   IMP ( COL (105 + _ajuste_x), .( TOTAL( 'DVIG' ) ) )
   DECIMALES:=2
   IMPRIME
   IMPRIME
   IMPRIME
FIN_SUB_RUTINA

INCLUYE PRESTAMO.DAT

SUB_RUTINA _imp_tot   
   FORMATO_PESOS:=0
   TABULA_KARDEX_NOMINA ( &kardex, 8 )
   SI ( _detalle_prestamos = 1 )
     checa_prestamos
   FIN_SI
   FORMATO_PESOS:=2
   _num_registro:=0
   REPITE
     trae_renglon
     SI ( _t_perc <> 0 )
       IMP ( COL(5 + _ajuste_x), $c_perc, COL(11 + _ajuste_x), $d_perc, COL(48 + _ajuste_x), DER($(_t_perc), 10) )
     FIN_SI
     SI ( _t_deduc <> 0 )
       IMP ( COL(65 + _ajuste_x), $c_deduc, COL(71 + _ajuste_x), $d_deduc, COL(108 + _ajuste_x), DER($(_t_deduc),10) )
     FIN_SI
     IMPRIME
     _num_registro:=_num_registro + 1
   HASTA(( _num_registro >= _total_conceptos ) OR (_t_perc + _t_deduc = 0))
   MIENTRAS(_num_registro < _total_conceptos )
     IMPRIME
     _num_registro:=_num_registro + 1     
   FIN_MIENTRAS
   #AQUI SE DEBE DEFINIR LO QUE SE DESEA IMPRIMIR SOBRE LAS PERCEPCIONES... SOLO RECUERDE RESTAR QUITAR UN RENGLON EN EL TOTAL DE CONCEPTOS...
FIN_SUB_RUTINA

SUB_RUTINA _pie
   IMP ( COL (11 + _ajuste_x), 'Total Percepciones', COL (48 + _ajuste_x), DER ($( TOTAL( 'PERC' ) ), 10) )
   IMP ( COL (71 + _ajuste_x), 'Total Deducciones', COL (108 + _ajuste_x), DER ($( TOTAL( 'DEDU' ) ), 10) )
   IMPRIME
   IMP ( COL (8 + _ajuste_x), SUBSTR('EMPRESA':'NOMBRE',1, 35) )
   IMPRIME
   IMP ( COL (108 + _ajuste_x), DER ($( TOTAL( 'TOTA' ) ), 10) )
   IMPRIME
FIN_SUB_RUTINA

_omitir_encabezados := 1
_omitir_subtotales := 1
_omitir_total_general := 1
_reporte_nomina := 1
lee_parametros_seleccion
lee_parametros_indices

#Definicion de variables en base a los parametros
SALIDA := $salida

$campos_extras:='E. RFC, E. AFILIACION'  
base_trabajadores

PRIMER_REGISTRO( 'EMPPRIN' )
MIENTRAS ( FIN_BASE('EMPPRIN') = FALSO )
  SI ( ( TOTAL( 'PERC' ) <> 0 ) OR ( TOTAL( 'DEDU' ) <> 0 ) )
    _enc
    _imp_tot
    _pie
    AVANZA_HOJA
  FIN_SI
  SIGUIENTE_REGISTRO('EMPPRIN')
FIN_MIENTRAS

termina_reporte
