#DEPURACION DE ACUMULADOS
#NO EDITAR
#USUARIO =*
#VERSION 2.500
#DESCRIPCION
#Este reporte borra todos los registros de acumulados donde el total = 0 para que no utilicen espacion.
#FIN_DESCRIPCION

NUEVO_PARAMETRO_SAL ( 'SALIDA', 'PANTALLA/IMPRESORA', $salida)
LEE_PARAMETROS
BORRA_PARAMETROS

FORMATO_FECHA := 2
SALIDA := $salida

ENCABEZADO   
   IMPRIME
   LETRA ( 'LETRA ENFATISADA' )
   IMP ( 'Rep. ', CLAVE_REPORTE, ' ', NOMBRE_REPORTE, COL ( 60 ), VERSION_GIRO ) IMPRIME
   IMP ( CENTRA ( 'EMPRESA':'NOMBRE' )) IMPRIME
   IMP ( CENTRA ( 'DEPURACION DE ACUMULADOS' )) IMPRIME
   IMP ( REPITETXT( '=',80 )) IMPRIME
   IMP ( COL(1), 'CLAVE', COL(20), 'CONCEPTO' ) IMPRIME
   IMP ( REPITETXT ('-', 80 )) IMPRIME
   LETRA ( 'LETRA NORMAL' )
   IMPRIME
FIN_ENCABEZADO

PIE
   IMPRIME ;; IMPRIME
   DECIMALES := 0
   LETRA ( 'LETRA ENFATISADA' )
   IMP ( COL ( 2 ), 'Fecha : ', FECHA (FECHA_HOY), ' ', 'Hora : ', HORA ( HORA_ACTUAL ), COL ( 65 ), 'PAGINA -', PAGINA, '-' ) IMPRIME
   LETRA ( 'LETRA NORMAL' ) IMPRIME
   DECIMALES := 2
FIN_PIE 

_acum := 0
_tot  := 0

ABRE_BASE( 'EMPACUM' )
MUESTRA_AVANCE( 'EMPACUM', 'PROGRESO...' )
PRIMER_REGISTRO ( 'EMPACUM' )
MIENTRAS ( FIN_BASE ( 'EMPACUM' ) = FALSO )
  _acum := 'EMPACUM':'ENE' + 'EMPACUM':'FEB' + 'EMPACUM':'MAR' + 'EMPACUM':'ABR' + 'EMPACUM':'MAY' + 'EMPACUM':'JUN'
  _acum := _acum + 'EMPACUM':'JUL' + 'EMPACUM':'AGO' + 'EMPACUM':'SEP' + 'EMPACUM':'OCT' + 'EMPACUM':'NOV' + 'EMPACUM':'DIC'
  SI (( _acum = 0 ) OR ( 'EMPACUM':'CLAVE' = '0000000000' ))
    IMP ( COL(1), 'EMPACUM':'CLAVE', COL(20), 'EMPACUM':'CONCEPTO' ) ;; IMPRIME
    _tot := _tot + 1
    BORRA_REGISTRO( 'EMPACUM' )
  SI_NO
    SIGUIENTE_REGISTRO ( 'EMPACUM' )
  FIN_SI
FIN_MIENTRAS

DECIMALES := 0
IMPRIME ;; IMPRIME
IMP ( 'TOTAL DE REGISTROS BORRADOS : ', _tot ) ;; IMPRIME
