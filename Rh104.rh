#CURSOS PROGRAMADOS
#VERSION 2.500
#USUARIO =*
#NO EDITAR
#DESCRIPCION
#Este reporte despliega los cursos programados en un rango de fechas dadas. Muestra ademas el intrucctor a impartir el curso.
#FIN_DESCRIPCION
NUEVO_PARAMETRO_SAL ('SALIDA','PANTALLA/IMPRESORA',$salida)
NUEVO_PARAMETRO_SEL ('ORDEN','CLAVE/FECHA/INSTRUCTOR',_orden)
NUEVO_PARAMETRO_FEC ('FECHA INICIAL DE PROGRAMACION','',_fi) 
NUEVO_PARAMETRO_FEC ('FECHA FINAL DE PROGRAMACION','',_ff)
LEE_PARAMETROS

FORMATO_FECHA := 1
SALIDA := $salida
DRAFT:='S'

INCLUYE ENCPIE.DAT

ENCABEZADO
   $titulo_reporte := 'CURSOS PROGRAMADOS DEL ' + FECHA ( _fi ) + ' AL ' + FECHA ( _ff )
   encabezado_ini
   IMP ( COL(1) , 'INICIA', COL(11), 'FINALIZA' )
   IMP ( COL(20), '  ENTRADA', COL(33), 'SALIDA' )
   IMP ( COL(45), 'CURSO PROGRAMADO...' ) ;; IMPRIME
   IMP ( COL(20), 'INSTRUCTOR', COL(50), 'LUGAR' )  ;; IMPRIME
   encabezado_fin
FIN_ENCABEZADO

PIE
   pie_estandar
FIN_PIE 

### DEFINICION DE LA TABLA
VAR_T ( sql )
AGREGA_VALOR_COL ( sql, 0, 'SELECT   CURSOPRO."FECHA INICIO", CURSOPRO."FECHA TERMINO",' )
AGREGA_VALOR_COL ( sql, 0, '         CURSOPRO."HORARIO ENTRADA", CURSOPRO."HORARIO SALIDA",' )
AGREGA_VALOR_COL ( sql, 0, '         CURSO.CLAVE AS CLAVE_CURSO, CURSO.DESCRIPCION AS DES_CURSO,' )
AGREGA_VALOR_COL ( sql, 0, '         INSTRUCT.CLAVE AS CLAVE_INSTRUCT, INSTRUCT. NOMBRE AS NOMBRE, CURSOPRO. LUGAR' )
SI ( SQL_SERVER )
  AGREGA_VALOR_COL ( sql, 0, 'FROM CURSO, CURSOPRO INNER JOIN INSTRUCT ON INSTRUCT.CLAVE = CURSOPRO. INSTRUCTOR' )
SI_NO
  AGREGA_VALOR_COL ( sql, 0, 'FROM CURSO, CURSOPRO, INSTRUCT RIGHT JOIN CURSOPRO ON INSTRUCT.CLAVE = CURSOPRO. INSTRUCTOR' ) 
FIN_SI
AGREGA_VALOR_COL ( sql, 0, 'WHERE CURSOPRO.CLAVE_CURSO = CURSO.CLAVE' )
AGREGA_VALOR_COL ( sql, 0, '   AND CURSOPRO."FECHA INICIO" >= :_fi' )
AGREGA_VALOR_COL ( sql, 0, '   AND CURSOPRO."FECHA INICIO" <= :_ff' )
SI ( _orden = 0 )
  AGREGA_VALOR_COL ( sql, 0, 'ORDER BY CURSO. CLAVE, CURSOPRO."FECHA INICIO"' )
SI_NO
   SI ( _orden = 1 )
     AGREGA_VALOR_COL ( sql, 0, 'ORDER BY CURSOPRO."FECHA INICIO", CLAVE' )
   SI_NO
     AGREGA_VALOR_COL ( sql, 0, 'ORDER BY CURSOPRO.INSTRUCTOR, CURSOPRO."FECHA INICIO"' ) 
   FIN_SI
FIN_SI
CREA_CONSULTA ( 'CONSULTA', sql )
PARAMETRO_SQL ( 'CONSULTA', '_fi', _fi )
PARAMETRO_SQL ( 'CONSULTA', '_ff', _ff )
PRIMER_REGISTRO ( 'CONSULTA' )
_total := 0

MIENTRAS ( FIN_BASE ( 'CONSULTA' ) = FALSO )
  IMP ( COL(1) , FECHA ( 'CONSULTA':'FECHA INICIO' ), COL(11), FECHA ( 'CONSULTA':'FECHA TERMINO' ))
  IMP ( COL(20), HORA ( 'CONSULTA':'HORARIO ENTRADA' ), COL(33), HORA ( 'CONSULTA':'HORARIO SALIDA' ))
  IMP ( COL(45), 'CONSULTA':'CLAVE_CURSO', COL(50), 'CONSULTA':'DES_CURSO' ) ;; IMPRIME
  IMP ( COL(20), 'CONSULTA':'CLAVE_INSTRUCT', COL(25), 'CONSULTA':'NOMBRE', COL(50), 'CONSULTA':'LUGAR' )  ;; IMPRIME
  _total := _total + 1
  SIGUIENTE_REGISTRO ( 'CONSULTA' )
FIN_MIENTRAS

DECIMALES := 0
IMPRIME
IMP ( COL (10), 'TOTAL DE TIPOS DE CURSOS : ', _total ) ;; IMPRIME
