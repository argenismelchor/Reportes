#CALCULO ANUAL DE ISPT
#NO EDITAR
#USUARIO =*
#TRABAJADORES VIGENTES
#VERSION 2.000
#DESCRIPCION
#Calculo anual de ISPT. Definir Tamaño PAPEL ESPECIAL a 350 mm * 279 mm desde Configuración de Impresoras, Papel, etc. desde WINDOWS.
#FIN_DESCRIPCION

NUEVO_PARAMETRO_SAL ( 'SALIDA' , 'PANTALLA/IMPRESORA/DISCO' , $salida )
NUEVO_PARAMETRO_FEC ( 'FECHA INICIAL' , '' , _fi ) 
NUEVO_PARAMETRO_FEC ( 'FECHA FINAL'   , '' , _ff )
NUEVO_PARAMETRO_TAB ( 'TABLA ISPT ANUAL' , 'TABISPT/CLAVE/TABLA DE IMPUESTOS' , $tispt ) 
NUEVO_PARAMETRO_TAB ( 'TABLA SUBS ANUAL' , 'TABISPT/CLAVE/TABLA DE IMPUESTOS' , $tsubs ) 
NUEVO_PARAMETRO_TAB ( 'TABLA CRED.ANUAL' , 'TABISPT/CLAVE/TABLA DE CREDITO'   , $tcred ) 
NUEVO_PARAMETRO_SEL ( 'DESEA ACUMULAR'   , 'N/S' , $acum )
LEE_PARAMETROS
BORRA_PARAMETROS

INCLUYE ENCPIE.DAT
_condensado := 1
COLUMNAS := 190

ENCABEZADO
   $titulo_reporte := 'Calculo Anual de Impuestos del ' + FECHA (_fi) + ' al ' + FECHA (_ff)
   encabezado_ini
   IMP ( COL (1)   , 'CLAVE' , COL (11) , 'NOMBRE COMPLETO' )
   IMP ( COL (45)  , DER ('PERCEP' , 12 ) , COL (57) , DER ( 'P GRAV' , 12 ) , COL (69) , DER ( 'EXENTO' , 12 ) , COL (81) , DER ( 'ISPT Ret' , 12 ) , COL (93) , DER ('CRED Ent' , 12 ) )
   IMP ( COL (105) , DER ('CALC ISPT' , 12 ) , COL (117) , DER ( 'CALC CRED' , 12 ) , COL (130) , DER ( 'ISPT Car' , 10 ) , COL (140) , DER ( 'ISPT Fav' , 10 ) , COL (150) , DER ( 'CRED Fav' , 10 ) )
   IMP ( COL (162) , 'F.Alta' , COL (178) , 'F.Baja' ) ;; IMPRIME
   encabezado_fin
FIN_ENCABEZADO

PIE
   pie_estandar
FIN_PIE

FORMATO_FECHA := 3
FORMATO_PESOS := 1
COLUMNAS      := 190
MODO_IMPRESION ( 'GRAFICO' )
TAMAÑO_HOJA ( 2790 , 3500 )

#Variables Globales
VAR_T ( &subtotales ) ;; VAR_N ( _aux1  ) ;; VAR_N ( _calc  ) ;; VAR_N ( _diff  ) ;; VAR_N ( _difc ) ;; VAR_N ( _subn )
VAR_N ( _dif  )       ;; VAR_N ( _credf ) ;; VAR_N ( _isptf ) ;; VAR_N ( _isptc ) ;; VAR_N ( _suba ) ;; VAR_N ( _tica )
VAR_N ( _tifa )       ;; VAR_N ( _tcfa  )
$tipo    := ''
_cont    := 0
_imp     := 0
_bandera := 0
_tot51   := 0
_tot51A  := 0

VAR_T ( &indices )
_num_ind := 0
$indice  := ''

# Subrutina diversas

SUB_RUTINA sumatoria
# Incremento de valores para los indices seleccionados
  _num_ind  := 0
  DECIMALES := 2
  REPITE
    INCREMENTA_TABLA ( &subtotales , 'PERC' , TRAE_VALOR_TABLA ( &indices , 0 , _num_ind ) , ACUM_ANUAL('PERC') )
    INCREMENTA_TABLA ( &subtotales , 'PGRA' , TRAE_VALOR_TABLA ( &indices , 0 , _num_ind ) , ACUM_ANUAL('PGRA') )
    INCREMENTA_TABLA ( &subtotales , 'EXEN' , TRAE_VALOR_TABLA ( &indices , 0 , _num_ind ) , ACUM_ANUAL('EXEN') )
    INCREMENTA_TABLA ( &subtotales , '51'   , TRAE_VALOR_TABLA ( &indices , 0 , _num_ind ) , ACUM_ANUAL('51')   )
    INCREMENTA_TABLA ( &subtotales , '51A'  , TRAE_VALOR_TABLA ( &indices , 0 , _num_ind ) , ACUM_ANUAL('51A')  )
    _num_ind := _num_ind + 1
  HASTA ( TRAE_VALOR_TABLA ( &indices , 0 , _num_ind ) = '' )
FIN_SUB_RUTINA

SUB_RUTINA acumula_ispt
   SI ( _isptf <> 0 )
      SI ( TRAE_REGISTRO ( 'EMPACUM' , 'EMPPRIN':'CLAVE' , '51' ) )
         'EMPACUM':'ESPECIAL' := _isptf 
      SI_NO
         AGREGA_REGISTRO ('EMPACUM')
         'EMPACUM':'CLAVE'    := 'EMPPRIN':'CLAVE'
         'EMPACUM':'CONCEPTO' := '51'
         'EMPACUM':'ESPECIAL' := _isptf 
      FIN_SI
      GRABA_BASE ('EMPACUM')
   FIN_SI
   SI ( _isptc <> 0 )
      SI ( TRAE_REGISTRO ( 'EMPACUM' , 'EMPPRIN':'CLAVE' , '51' ) )
         'EMPACUM':'ESPECIAL' := _isptc 
      SI_NO
         AGREGA_REGISTRO ('EMPACUM')
         'EMPACUM':'CLAVE'    := 'EMPPRIN':'CLAVE'
         'EMPACUM':'CONCEPTO' := '51'
         'EMPACUM':'ESPECIAL' := _isptc 
      FIN_SI
      GRABA_BASE ('EMPACUM')
   FIN_SI
   SI ( _credf <> 0 )
      SI ( TRAE_REGISTRO ( 'EMPACUM' , 'EMPPRIN':'CLAVE' , '51A' ) )
        'EMPACUM':'ESPECIAL' := _credf  
      SI_NO
        AGREGA_REGISTRO ('EMPACUM')
        'EMPACUM':'CLAVE'    := 'EMPPRIN':'CLAVE'
        'EMPACUM':'CONCEPTO' := '51A'
        'EMPACUM':'ESPECIAL' := _credf 
      FIN_SI
      GRABA_BASE ('EMPACUM')
   FIN_SI
# Si los conceptos SUBSIDIO ACREDITABLE Y NO ACREDITABLE no existen en la Tabla de Conceptos, marcar las
# siguientes líneas como comentario ...
   SI ( _suba <> 0 )
      SI ( TRAE_REGISTRO ( 'EMPACUM' , 'EMPPRIN':'CLAVE' , 'SUBS' ) )
         'EMPACUM':'ESPECIAL' := _suba 
      SI_NO
         AGREGA_REGISTRO ('EMPACUM')
         'EMPACUM':'CLAVE'    := 'EMPPRIN':'CLAVE'
         'EMPACUM':'CONCEPTO' := 'SUBS'
         'EMPACUM':'ESPECIAL' := _suba 
      FIN_SI
      GRABA_BASE ('EMPACUM')
   FIN_SI
   SI ( _subn <> 0 )
      SI ( TRAE_REGISTRO ( 'EMPACUM' , 'EMPPRIN':'CLAVE' , 'SUBN' ) )
        'EMPACUM':'ESPECIAL' := _subn 
      SI_NO
        AGREGA_REGISTRO ('EMPACUM')
        'EMPACUM':'CLAVE'    := 'EMPPRIN':'CLAVE'
        'EMPACUM':'CONCEPTO' := 'SUBN'
        'EMPACUM':'ESPECIAL' := _subn 
      FIN_SI
      GRABA_BASE ('EMPACUM')
   FIN_SI
FIN_SUB_RUTINA

SUB_RUTINA formato_trabajador
   LETRA ( 'LETRA CONDENSADA' )
   IMP ( COL (1)  , 'EMPPRIN':'CLAVE' )
   IMP ( COL (11) , SUBSTR ( 'EMPPRIN':'NOMBRE' , 1 , 33 ) ) 
   DECIMALES:=2
   IMP ( COL (45) , DER ( $ (ACUM_ANUAL ('PERC')) , 12 ) )
   IMP ( COL (57) , DER ( $ (ACUM_ANUAL ('PGRA')) , 12 ) )
   IMP ( COL (69) , DER ( $ (ACUM_ANUAL ('EXEN')) , 12 ) )
   IMP ( COL (81) , DER ( $ (ACUM_ANUAL ('51'))   , 12 ) )
   IMP ( COL (93) , DER ( $ (ACUM_ANUAL ('51A'))  , 12 ) )
   _suba  := 0
   _subn  := 0
   _calc  := 0
   _dif   := 0
   _isptc := 0
   _isptf := 0
   _credf := 0
   SI (( FUE_VIGENTE ( _ff , _ff ) ) AND ( 'EMPPRIN':'INGRESO' <= _fi ))
      _aux1  := ACUM_ANUAL ('PGRA')
      SI ( TRAE_REGISTRO ( 'TABISPT' , $tispt ) )
         _calc := CALCULA_ISPT (_aux1)
      FIN_SI
      SI ( TRAE_REGISTRO ( 'TABISPT' , $tsubs ) )
         _calc := _calc - CALCULA_SUBSIDIO (_aux1)
         DECIMALES := 8
         _suba := CALCULA_SUBSIDIO (_aux1)
         _subn := ( CALCULA_SUBSIDIO (_aux1) / ( 'TABISPT':'SUBSIDIO' / 100 ) )
         _subn := _subn - _suba
         _suba := _suba - ACUM_ANUAL ('SUBS')
         _subn := _subn - ACUM_ANUAL ('SUBN')
         DECIMALES := 2
      FIN_SI
      SI ( TRAE_REGISTRO ('TABISPT' , $tcred ) )
         _calc := _calc - CALCULA_ISPT (_aux1)
      FIN_SI
# Resultado Anual : CREDITO AL SALARIO
      SI ( _calc < 0 )
         IMP ( COL (117) , DER ( $ (_calc) , 12 ) )
         _tot51A := _tot51A + _calc          
         _dif   := _calc - ACUM_ANUAL ('51A') 
         _isptf := ACUM_ANUAL ('51') * -1    
         SI ( _dif < 0 )
            _credf := _dif
         SI_NO
            _isptf := _isptf + _dif
            SI ( _isptf > 0 )
               _isptc := _isptf
               _isptf := 0
            FIN_SI
         FIN_SI
      SI_NO
# Resultado Anual : IMPUESTO
         IMP ( COL (105) , DER ( $ (_calc) , 12 ) )
         _tot51 := _tot51 + _calc
         _dif := _calc - ACUM_ANUAL ('51')
         _dif := _dif  + ( ACUM_ANUAL ('51A') * -1 )
         SI ( _dif < 0 )
            _isptf := _dif 
         SI_NO
            _isptc := _dif 
         FIN_SI
      FIN_SI
      SI ( _isptc <> 0)
         IMP ( COL (130) , DER ( $ (_isptc ) , 10 ) )
      FIN_SI
      SI ( _isptf <> 0)   
         IMP ( COL (140) , DER ( $ (_isptf ) , 10 ) )   
      FIN_SI
      SI ( _credf <> 0)
         IMP ( COL (150) , DER ( $ (_credf ) , 10 ) )   
      FIN_SI
   SI_NO
      IMP ( COL (105) , DER ( $ (ACUM_ANUAL ('51'))  , 12 ) )
      IMP ( COL (117) , DER ( $ (ACUM_ANUAL ('51A')) , 12 ) )
      _tot51  := _tot51  + ACUM_ANUAL ('51')
      _tot51A := _tot51A + ACUM_ANUAL ('51A')
   FIN_SI
   IMP ( COL (162) , FECHA ('EMPPRIN':'INGRESO') )
   ULTIMO_REGISTRO ( 'EMPSDO' )
   SI (( 'EMPSDO':'TIPO' = 'B' ) AND ( 'EMPSDO':'FECHA' >= _fi ) AND ( 'EMPSDO':'FECHA' <= _ff ))
      IMP ( COL (178) , FECHA ('EMPSDO':'FECHA') )
   FIN_SI
   IMPRIME
   _tica   := _tica + _isptc
   _tifa   := _tifa + _isptf
   _tcfa   := _tcfa + _credf
   SI ( $acum = 'S' ) 
      acumula_ispt  
   FIN_SI            
FIN_SUB_RUTINA

SUB_RUTINA encabezados
  #
FIN_SUB_RUTINA

SUB_RUTINA sub_totales
   IMPRIME
   IMP ( COL (11) , 'SubTotales  ' , $indice )
   DECIMALES := 2
   IMP ( COL (45)  , DER ( $ (VALOR_TABLA ( &subtotales , 'PERC' , $indice )) , 12 ) )
   IMP ( COL (57)  , DER ( $ (VALOR_TABLA ( &subtotales , 'PGRA' , $indice )) , 12 ) )
   IMP ( COL (69)  , DER ( $ (VALOR_TABLA ( &subtotales , 'EXEN' , $indice )) , 12 ) )
   IMP ( COL (81)  , DER ( $ (VALOR_TABLA ( &subtotales , '51'   , $indice )) , 12 ) )
   IMP ( COL (93)  , DER ( $ (VALOR_TABLA ( &subtotales , '51A'  , $indice )) , 12 ) )
   SI ( $indice = 'TIPO DE NOMINA' )
      IMP ( COL (105) , DER ( $ (_tot51 ), 10 ) )
      IMP ( COL (117) , DER ( $ (_tot51A), 10 ) )
      IMP ( COL (130) , DER ( $ (_tica)  , 10 ) )
      IMP ( COL (140) , DER ( $ (_tifa)  , 10 ) )
      IMP ( COL (150) , DER ( $ (_tcfa)  , 10 ) )
   FIN_SI
   IMPRIME
   IMPRIME
   ASIGNA_VALOR_TABLA ( &subtotales , 'PERC' , $indice , 0 )
   ASIGNA_VALOR_TABLA ( &subtotales , 'PGRA' , $indice , 0 )
   ASIGNA_VALOR_TABLA ( &subtotales , 'EXEN' , $indice , 0 )
   ASIGNA_VALOR_TABLA ( &subtotales , '51'   , $indice , 0 )
   ASIGNA_VALOR_TABLA ( &subtotales , '51A'  , $indice , 0 )
FIN_SUB_RUTINA

INCLUYE UTIL2.DAT

_reporte_nomina := 1
lee_parametros_seleccion
lee_parametros_indices

$ind_act := ''
$nom_ind := ''

# Definicion de la tabla de indices utilizados
REPITE
  $nom_ind := TRAE_VALOR_TABLA( &in , 2 , _num_ind )
  $ind_act := TRAE_VALOR_TABLA( &in , 0 , _num_ind )
  SI (( $nom_ind <> '' ) AND ( $ind_act = '1' ))
    AGREGA_VALOR_COL ( &indices , 0 , $nom_ind )
  FIN_SI
  _num_ind := _num_ind + 1
HASTA ( $nom_ind = '' )
AGREGA_VALOR_COL ( &indices , 0 , 'TOTAL' )

# Definicion de variables en base a los parametros
SALIDA := $salida

base_trabajadores

USA_ARCHIVO ( 'EMPPRIN' , 'EMPACUM' , 'CLAVE' )
USA_ARCHIVO ( 'EMPPRIN' , 'EMPSDO'  , 'CLAVE' )

MIENTRAS ( FIN_BASE ('EMPPRIN') = FALSO )
    SI ( ACUM_ANUAL ('PGRA') <> 0 )
       sumatoria
       datos_trabajador
    FIN_SI
    SIGUIENTE_REGISTRO ('EMPPRIN')
FIN_MIENTRAS

termina_reporte
